-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Equity.Instrument where

import DA.Set (singleton)
import Daml.Finance.Equity.Lifecycle.Distribution qualified as Lifecycle (Distribution(..))
import Daml.Finance.Equity.Lifecycle.Merger qualified as Lifecycle (Merger(..))
import Daml.Finance.Equity.Lifecycle.StockSplit qualified as Lifecycle (StockSplit(..))
import Daml.Finance.Equity.Lifecycle.Takeover qualified as Lifecycle (Takeover(..))
import Daml.Finance.Interface.Asset.Instrument qualified as Instrument (I, View(..), disclosureUpdateReference)
import Daml.Finance.Interface.Asset.Types (Id, InstrumentKey(..))
import Daml.Finance.Interface.Common.Disclosure qualified as Disclosure (I, SetObservers(..), View(..))
import Daml.Finance.Interface.Common.Types (Observers)
import Daml.Finance.Interface.Common.Util (flattenObservers)
import Daml.Finance.Interface.Equity.Instrument qualified as Equity (Distribution(..), HasImplementation, I, Merger(..), StockSplit(..), Takeover(..), View(..))

type T = Instrument

instance Equity.HasImplementation T
-- | An Instrument representing a common stock.
template Instrument
  with
    issuer : Party
      -- ^ Issuer.
    depository : Party
      -- ^ Depository.
    id : Id
      -- ^ A textual identifier.
    observers : Observers
      -- ^ Observers.
    validAsOf : Time
      -- ^ Timestamp as of which the instrument is valid. This usually coincides with the timestamp of the event that creates the instrument. It usually does not coincide with ledger time.
  where
    signatory depository, issuer
    observer flattenObservers observers

    let instrumentKey = InstrumentKey with depository; issuer; id

    implements Instrument.I where
      asDisclosure = toInterface @Disclosure.I this
      view = Instrument.View with issuer; depository; id; validAsOf
      getKey = instrumentKey

    implements Equity.I where
      asInstrument = toInterface @Instrument.I this
      view = Equity.View ()
      distributionImpl Equity.Distribution{id; perUnitDistribution; effectiveDate; newInstrument} key =
        coerceContractId <$>
          create Lifecycle.Distribution with issuer; effectiveDate; perUnitDistribution; currentInstrument = key; newInstrument; id; observers
      stockSplitImpl Equity.StockSplit{id; factor; newInstrument; effectiveDate} key =
        coerceContractId <$>
          create Lifecycle.StockSplit with issuer; currentInstrument = key; factor; newInstrument; effectiveDate; id; observers
      mergerImpl Equity.Merger{id; offerer; mergingInstrument; emergingQuantity; expirationDate} key  =
        coerceContractId <$>
          create Lifecycle.Merger with offerer; executionDate = expirationDate; mergingInstrument; emergingQuantity; id; observers
      takeoverImpl Equity.Takeover{id; offerer; perUnitPayout; effectiveDate} key =
        coerceContractId <$>
          create Lifecycle.Takeover with offerer; executionDate = effectiveDate; targetInstrument = key; offeredAssets = perUnitPayout; id; observers

    implements Disclosure.I where
      view = Disclosure.View with disclosureControllers = singleton $ singleton issuer; observers
      setObserversImpl Disclosure.SetObservers{newObservers} = do
        cid <- toInterfaceContractId <$> create this with observers = newObservers
        Instrument.disclosureUpdateReference newObservers instrumentKey cid
      archiveImpl self = archive (coerceContractId self : ContractId Instrument)
