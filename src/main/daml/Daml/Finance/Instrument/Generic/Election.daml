-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Generic.Election where

import DA.Map qualified as M (fromList)
import DA.Set (fromList, singleton)
import Daml.Finance.Interface.Common.Disclosure qualified as Disclosure (I, SetObservers(..), View(..))
import Daml.Finance.Common.Shared.Types.Util (Observers)
import Daml.Finance.Interface.Common.Util (flattenObservers, verify)
import Daml.Finance.Interface.Holding.Base qualified as Base (I)
import Daml.Finance.Interface.Holding.Util (getAmount, getCustodian, getInstrument, getOwner)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (K)
import Daml.Finance.Interface.Instrument.Generic.Election qualified as Election (HasImplementation, I, View(..))
import Daml.Finance.Interface.Instrument.Generic.Types (C)
import Daml.Finance.Interface.Lifecycle.Event qualified as Event (I, View(..))

-- | Type synonym for `Election`.
type T = Election

instance Election.HasImplementation T
-- | An election, such as the exercise of an option.
template Election
  with
    elector : Party
      -- ^ Entity making the election.
    counterparty : Party
      -- ^ Faces the `elector` in the `Holding`.
    provider : Party
      -- ^ The provider of the election is an entity that has the authority to process the election and create a new instrument version.
    id : Text
      -- ^ A textual identifier.
    instrument : Instrument.K
      -- ^ The instrument to which the election applies.
    amount : Decimal
      -- ^ Number of units of instrument to which the election applies.
    claim : C
      -- ^ the elected sub-tree.
    electorIsOwner : Bool
      -- ^ `True` if the elector is the owner of a claim, `False` otherwise.
    electionTime : Time
      -- ^ Time at which the election is put forward.
    observers : Observers
      -- ^ A set of observers.
  where
    signatory elector, provider
    observer flattenObservers observers

    implements Event.I where
      view = Event.View with eventTime = electionTime

    implements Election.I where
      asEvent = toInterface @Event.I this
      view = Election.View with amount; elector; counterparty; electorIsOwner; observers; provider; instrument; claim
      archive' self = archive (coerceContractId self : ContractId Election)

    implements Disclosure.I where
      view = Disclosure.View with disclosureControllers = singleton $ singleton elector; observers
      setObservers Disclosure.SetObservers{newObservers} =
        toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId Election)

-- | Helper contract to delegate the right to create `Election`\s referencing a specific `Instrument`.
-- The provider delegates the ability to create `Election`\s to any party that has visibility on the `ElectionFactory` contract. In order to create the `Election`, a valid `Holding` must be presented which identifies the choice controller as either the owner or the custodian to the `Holding`.
template ElectionFactory
  with
    provider : Party
      -- ^ provider
    id : Text
      -- ^ a textual identifier
    claim : C
      -- ^ the elected sub-tree
    observers : Observers
      -- ^ observers of the contract
    instrument : Instrument.K
      -- ^ key of the instrument to which the election applies
  where
    signatory provider
    observer flattenObservers observers

    nonconsuming choice CreateElection : ContractId Election
      -- ^ Creates an `Election` on an instrument. This choice must be exercised by the `elector`.
      with
        elector : Party
          -- ^ Entity making the election.
        electionTime : Time
          -- ^ Time at which the election is put forward.
        holdingCid : ContractId Base.I
          -- ^ A holding used to verify that the elector is entitled to make the election.
        amount : Decimal
          -- ^ Number of units of instrument to which the election applies.
      controller elector
      do
        holding <- fetch holdingCid
        verify (getInstrument holding == instrument) "Invalid holding presented : reference instrument"
        verify (getAmount holding >= amount) $ "Invalid holding presented : amount is " <> show (getAmount holding) <> " but elected amount is " <> show amount
        let
          custodian = getCustodian holding
          owner = getOwner holding
          electorIsOwner
            | elector == owner = True
            | elector == custodian = False
            | otherwise = error "Election can be made only on behalf of the owner or the custodian"
        create Election
          with
            id
            claim
            electionTime
            electorIsOwner
            elector
            counterparty = if electorIsOwner then custodian else owner
            instrument
            amount
            observers = M.fromList [("Holders", fromList [singleton custodian, singleton owner, singleton provider] )]
            provider
