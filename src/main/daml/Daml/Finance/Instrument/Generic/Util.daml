-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Generic.Util where

import ContingentClaims.Claim (Claim(..), Inequality(..), give, one, scale, upTo, when, until)
import ContingentClaims.Observation (Observation(..))
import DA.Date
import DA.List (head, (!!))
import DA.Optional (fromOptional, fromSome, isNone, isSome)
import DA.Record (HasField)
import DA.Set (fromList)
import DA.Text (sha256)
import Daml.Finance.Data.Reference.HolidayCalendar (GetCalendar(..), HolidayCalendar, HolidayCalendarKey(..))
import Daml.Finance.Interface.Claims.Claim qualified as Claim (I, getAcquisitionTime)
import Daml.Finance.Interface.Claims.Types (C, Deliverable, Observable, TaggedClaim(..))
import Daml.Finance.Interface.Claims.Util (isZero', toTime')
import Daml.Finance.Interface.Claims.Util.Lifecycle (lifecycle, lifecycleClaims, net, splitPending, timeEvent)
import Daml.Finance.Interface.Data.Observable qualified as Observable (I)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (I, createReference, getKey)
import Daml.Finance.Interface.Lifecycle.Clock qualified as Clock (I)
import Daml.Finance.Interface.Lifecycle.Effect qualified as Effect (I)
import Daml.Finance.Interface.Lifecycle.Event qualified as Event (I)
import Daml.Finance.Interface.Lifecycle.Rule.Lifecycle qualified as Lifecycle (I)
import Daml.Finance.Interface.Types.Common (Id, Parties)
import Daml.Finance.Interface.Types.Date.Calendar
import Daml.Finance.Interface.Types.Date.Classes (toUTCTime)
import Daml.Finance.Interface.Types.Date.DayCount (DayCountConventionEnum)
import Daml.Finance.Interface.Types.Date.RollConvention
import Daml.Finance.Interface.Types.Date.Schedule (Frequency(..), PeriodicSchedule(..), Schedule, SchedulePeriod, StubPeriodTypeEnum(..))
import Daml.Finance.Interface.Types.Fpml
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, flattenObservers)
import Daml.Finance.Lifecycle.Effect (Effect(..))
import Daml.Finance.Lifecycle.Types (Unit(..))
import Daml.Finance.Util.Date.Calendar (addBusinessDays, merge)
import Daml.Finance.Util.Date.Calendar (adjustDate)
import Daml.Finance.Util.Date.DayCount (calcPeriodDcf)
import Daml.Finance.Util.Date.RollConvention (addPeriod)
import Daml.Finance.Util.Date.Schedule (createSchedule)
import Prelude hiding (key)

-- | Retrieve holiday calendar(s) from the ledger
getHolidayCalendars : [Text] -> Party -> Party -> Update [HolidayCalendarData]
getHolidayCalendars holidayCalendarIds issuer calendarDataAgency = do
  let
    -- get a holiday calendar from the ledger
    getCalendar holidayCalendarId = do
      exerciseByKey @HolidayCalendar holCalKey GetCalendar with viewer = issuer where
        holCalKey = HolidayCalendarKey with
          agency = calendarDataAgency
          entity = holidayCalendarId
  -- Get the holiday calendars
  mapA getCalendar holidayCalendarIds

-- | Retrieve holiday calendar(s) from the ledger and roll out the payment schedule.
rollPaymentSchedule : PeriodicSchedule -> [Text] -> Party -> Party -> Update Schedule
rollPaymentSchedule periodicSchedule holidayCalendarIds issuer calendarDataAgency = do
  cals <- getHolidayCalendars holidayCalendarIds issuer calendarDataAgency
  pure $ createSchedule cals periodicSchedule

-- | Convert the claims to UTCTime and tag them.
prepareAndTagClaims : [Claim Date Decimal Deliverable Observable] -> Text -> TaggedClaim
prepareAndTagClaims cs tag = do
  let claim = mapClaimToUTCTime $ mconcat cs
  TaggedClaim with tag; claim

-- FIXED_RATE_BOND_COUPON_CLAIMS_BEGIN
createFixRatePaymentClaimsList : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Deliverable -> [Claim Date Decimal Deliverable Observable]
createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf couponRate issuerPays dayCountConvention cashInstrumentCid = do
  let
    couponDatesAdjusted = map (.adjustedEndDate) schedule
    couponAmounts = map (\p -> couponRate * (calcPeriodDcf dayCountConvention p useAdjustedDatesForDcf periodicSchedule.terminationDate periodicSchedule.frequency)) schedule
    couponClaimAmounts = mconcat $ zipWith (\d a -> when (TimeGte d) $ scale (Const a) $ one cashInstrumentCid) couponDatesAdjusted couponAmounts
    couponClaims = if issuerPays then couponClaimAmounts else give couponClaimAmounts
  pure couponClaims
-- FIXED_RATE_BOND_COUPON_CLAIMS_END

-- | Calculate a fix rate amount for each payment date and create claims.
createFixRatePaymentClaims : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Deliverable -> TaggedClaim
createFixRatePaymentClaims schedule periodicSchedule useAdjustedDatesForDcf couponRate issuerPays dayCountConvention cashInstrumentCid = do
  let couponClaims = createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf couponRate issuerPays dayCountConvention cashInstrumentCid
  prepareAndTagClaims couponClaims "Fix rate payment"

-- | Calculate a fix rate amount (if a credit event has not yet happened) for each payment date and create claims.
createConditionalCreditFixRatePaymentClaims : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Deliverable -> Observable -> TaggedClaim
createConditionalCreditFixRatePaymentClaims schedule periodicSchedule useAdjustedDatesForDcf couponRate issuerPays dayCountConvention cashInstrumentCid defaultProbabilityReferenceId = do
  let
    couponClaims = createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf couponRate issuerPays dayCountConvention cashInstrumentCid
    creditEvent = Lte (Const 1.0, Observe defaultProbabilityReferenceId)
    couponClaimUntilCreditEvent = until creditEvent $ mconcat couponClaims
  prepareAndTagClaims [couponClaimUntilCreditEvent] "Fix rate payment (unless credit event has occurred)"

-- | Calculate a (1-recoveryRate) payment if a credit event just happened and create claims.
createCreditEventPaymentClaims : Bool -> Deliverable -> Observable -> Observable -> PeriodicSchedule -> TaggedClaim
createCreditEventPaymentClaims issuerPays cashInstrumentCid defaultProbabilityReferenceId recoveryRateReferenceId periodicSchedule = do
  let
    creditEvent = Lte (Const 1.0, Observe defaultProbabilityReferenceId)
    payoffAmount = scale (Const 1.0 - Observe recoveryRateReferenceId) $ one cashInstrumentCid
    payoff = if issuerPays then payoffAmount else give payoffAmount
    creditEventClaim =
      when (TimeGte periodicSchedule.effectiveDate)
        $ when creditEvent
        $ when (upTo periodicSchedule.terminationDate)
        payoff
  prepareAndTagClaims [creditEventClaim] "Credit event payment"

-- FLOATING_RATE_BOND_COUPON_CLAIMS_BEGIN
-- | Calculate a floating rate amount for each payment date and create claims.
-- The floating rate is always observed on the first day of each payment period and used for the corresponding payment on the last day of that payment period.
-- This means that the calculation agent needs to provide such an Observable, irrespective of
-- the kind of reference rate used (e.g. a forward looking LIBOR or a backward looking SOFR-COMPOUND).
createFloatingRatePaymentClaims : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Deliverable -> Observable -> TaggedClaim
createFloatingRatePaymentClaims schedule periodicSchedule useAdjustedDatesForDcf floatingRateSpread issuerPays dayCountConvention cashInstrumentCid referenceRateId = do
  let
    couponClaimAmounts = mconcat $ map (\p ->
      when (TimeGte p.adjustedStartDate) $ scale ((Observe referenceRateId + Const floatingRateSpread) * (Const (calcPeriodDcf dayCountConvention p useAdjustedDatesForDcf periodicSchedule.terminationDate periodicSchedule.frequency))) $
      when (TimeGte p.adjustedEndDate) $ one cashInstrumentCid) schedule
    couponClaims = if issuerPays then couponClaimAmounts else give couponClaimAmounts
  prepareAndTagClaims [couponClaims] "Floating rate payment"
-- FLOATING_RATE_BOND_COUPON_CLAIMS_END

-- ASSET_PERFORMANCE_CLAIMS_BEGIN
-- | Calculate the asset performance for each payment date and create claims.
-- The performance is calculated using the reference asset from the start date to the end date of each payment period.
-- The reference asset Observable needs to contain the appropriate type of fixings:
--   unadjusted fixings in case of a price return asset swap
--   adjusted fixings in case of a total return asset swap
createAssetPerformancePaymentClaims : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Bool -> DayCountConventionEnum -> Deliverable -> Observable -> TaggedClaim
createAssetPerformancePaymentClaims schedule periodicSchedule useAdjustedDatesForDcf issuerPays dayCountConvention cashInstrumentCid referenceAssetId = do
  let
    assetClaimAmounts = mconcat $ map (\p ->
      when (TimeGte p.adjustedStartDate) $ scale (Const 1.0 / Observe referenceAssetId) $
      when (TimeGte p.adjustedEndDate) $ scale (Observe referenceAssetId) $ one cashInstrumentCid) schedule
    correctionClaimAmounts = mconcat $ map (\p ->
      when (TimeGte p.adjustedEndDate) $ one cashInstrumentCid) schedule
    assetClaims = if issuerPays then assetClaimAmounts else give assetClaimAmounts
    correctionAssetClaims = if issuerPays then give correctionClaimAmounts else correctionClaimAmounts
  prepareAndTagClaims [assetClaims, correctionAssetClaims] "Asset performance payment"
-- ASSET_PERFORMANCE_CLAIMS_END

-- FIXED_RATE_BOND_REDEMPTION_CLAIM_BEGIN
-- | Create an FX adjusted principal claim.
-- This can be used for both FX swaps (using the appropriate FX rate) and single currency bonds (setting the FX rate to 1.0).
createFxAdjustedPrincipalClaim : Bool -> Decimal -> Deliverable -> Date -> TaggedClaim
createFxAdjustedPrincipalClaim issuerPays fxRateMultiplier cashInstrumentCid valueDate = do
  let
    fxLegClaimAmount = when (TimeGte valueDate) $ scale (Const fxRateMultiplier) $ one cashInstrumentCid
    fxLegClaim = if issuerPays then fxLegClaimAmount else give fxLegClaimAmount
  prepareAndTagClaims [fxLegClaim] "Principal payment"
-- FIXED_RATE_BOND_REDEMPTION_CLAIM_END

-- | Create a schedule for periodic payments.
createPaymentPeriodicSchedule : SwapStream -> PeriodicSchedule
createPaymentPeriodicSchedule s = do
  let
    periodicSchedule = PeriodicSchedule with
      businessDayAdjustment =
        BusinessDayAdjustment with
          calendarIds = s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessCenters
          convention = s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessDayConvention
      effectiveDateBusinessDayAdjustment = None
      terminationDateBusinessDayAdjustment = None
      frequency =
        Frequency with
          rollConvention = s.calculationPeriodDates.calculationPeriodFrequency.rollConvention
          period = s.calculationPeriodDates.calculationPeriodFrequency.period
          periodMultiplier = s.calculationPeriodDates.calculationPeriodFrequency.periodMultiplier
      effectiveDate = s.calculationPeriodDates.effectiveDate
      firstRegularPeriodStartDate = s.calculationPeriodDates.firstRegularPeriodStartDate
      lastRegularPeriodEndDate = s.calculationPeriodDates.lastRegularPeriodEndDate
      stubPeriodType = None
      terminationDate = s.calculationPeriodDates.terminationDate
  periodicSchedule

-- | Create claims from swapStream that describes a fix coupon stream.
calculateClaimsFromFixSwapStream : SwapStream -> PeriodicSchedule -> [SchedulePeriod] -> Bool -> Bool -> Deliverable -> Update TaggedClaim
calculateClaimsFromFixSwapStream s periodicSchedule streamSchedule useAdjustedDatesForDcf issuerPaysLeg currency = do
  let f = fromSome s.calculationPeriodAmount.calculation.fixedRateSchedule
  pure $ createFixRatePaymentClaims streamSchedule periodicSchedule useAdjustedDatesForDcf f.initialValue issuerPaysLeg s.calculationPeriodAmount.calculation.dayCountFraction currency

-- | Rule when one specific floating rate is provided for a stub period.
getSingleStubRateFromFpml floatingRate = do
  Some (Observe floatingRate.floatingRateIndex)

-- | Rule when two specific floating rates are provided for a stub period.
-- In this case, linear interpolation should be used, as specified here:
-- https://www.isda.org/a/aWkgE/Linear-interpolation-04022022.pdf
getInterpolatedStubRateFromFpml fr1 fr2 p cal convention = do
  let
    tenor1 = fromSome fr1.indexTenor
    tenor2 = fromSome fr2.indexTenor
    s = p.adjustedStartDate
    p1 = adjustDate cal convention (addPeriod s tenor1)
    p2 = adjustDate cal convention (addPeriod s tenor2)
    tn = subDate p.adjustedEndDate s
    t1 = subDate p1 s
    t2 = subDate p2 s
    d1 = tn - t1
    d2 = t2 - tn
    w1 = intToDecimal d2 / intToDecimal (d1 + d2)
    w2 = 1.0 - w1
  -- In the ISDA paper, the following expression is used for linear interpolation:
  --Some (Observe (fr1.floatingRateIndex) + (Observe (fr2.floatingRateIndex) - Observe (fr1.floatingRateIndex)) * Const (intToDecimal (tn - t1) / intToDecimal (t2 - t1)))
  -- This expression can be simplified to:
  Some (Const w1 * Observe (fr1.floatingRateIndex) + Const w2 * Observe (fr2.floatingRateIndex))

-- | Get the floating stub rate to be used for a stub period.
getStubRateFloatingFromFpml floatingRates p cal convention = do
  if length floatingRates == 1 then
    getSingleStubRateFromFpml (head floatingRates)
  else if length floatingRates == 2 then
    getInterpolatedStubRateFromFpml (floatingRates !! 0) (floatingRates !! 1) p cal convention
  else
    error "only one or two floating stub rates supported"

-- | Get the stub rate to be used for a stub period.
-- Currently, three main options from the FpML schema are supported:
-- 1. A fix stubRate.
-- 2. One or two floating rates for the stub.
-- 3. No specific stub rate defined -> use the same rate as is used for regular periods.
getStubRateFromFpml sc initialStub p cal convention = do
  let
    stubUsed = if initialStub then sc.initialStub else sc.finalStub
    is = fromSome stubUsed
  if isSome is.stubRate then
    if isNone is.floatingRate then Some (Const (fromSome is.stubRate)) else error "both stubRate and floatingRate provided"
  else if isSome is.floatingRate then
    if isNone is.stubRate then getStubRateFloatingFromFpml (fromSome is.floatingRate) p cal convention else error "both stubRate and floatingRate provided"
  else
    None

-- | Calculate a floating rate amount for each payment date and create claims.
-- The floating rate is observed according to the FpML ResetDates component and used for the corresponding payment on the last day of that payment period.
-- This function is under construction and can probably be merged with createFloatingRatePaymentClaims above once all necessary FpML features have been implemented.
createFloatingRatePaymentClaimsFpml : [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Deliverable -> Observable -> HolidayCalendarData -> SwapStream -> Update TaggedClaim
createFloatingRatePaymentClaimsFpml schedule periodicSchedule useAdjustedDatesForDcf floatingRateSpread issuerPays dayCountConvention cashInstrumentCid referenceRateId fixingCalendars s = do
  let
    resetDates = fromSome s.resetDates
    bdc = s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessDayConvention
  assertMsg "Only daily fixing date period supported" (resetDates.fixingDates.period == D)
  assertMsg "Only Business day type supported" (resetDates.fixingDates.dayType == Business)
  assertMsg "No adjustement (besides business day type) supported" (resetDates.fixingDates.businessDayConvention == NoAdjustment)
  let
    fixingDates = map (\p -> addBusinessDays fixingCalendars resetDates.fixingDates.periodMultiplier p.adjustedStartDate) schedule
    createClaim (p,f) =
      when (TimeGte f) $ scale (rate * (Const (calcPeriodDcf dayCountConvention p useAdjustedDatesForDcf periodicSchedule.terminationDate periodicSchedule.frequency))) $
      when (TimeGte p.adjustedEndDate) $ one cashInstrumentCid
        where
          regularRate = (Observe referenceRateId + Const floatingRateSpread)
          rate =
            if isNone p.stubType || isNone s.stubCalculationPeriodAmount then regularRate
            else if fromSome p.stubType == LongInitial || fromSome p.stubType == ShortInitial then
              fromOptional regularRate (getStubRateFromFpml (fromSome s.stubCalculationPeriodAmount) True p fixingCalendars bdc)
            else if fromSome p.stubType == LongFinal || fromSome p.stubType == ShortFinal then
              fromOptional regularRate (getStubRateFromFpml (fromSome s.stubCalculationPeriodAmount) False p fixingCalendars bdc)
            else error "stub type not supported"
    floatingClaimAmounts = mconcat $ map createClaim $ zip schedule fixingDates
    floatingClaims = if issuerPays then floatingClaimAmounts else give floatingClaimAmounts
  pure $ prepareAndTagClaims [floatingClaims] "Floating rate payment"

-- | Create claims from swapStream that describes a floating coupon stream.
calculateClaimsFromFloatingSwapStream : SwapStream -> PeriodicSchedule -> [SchedulePeriod] -> Bool -> Bool -> Deliverable -> Party -> Party -> Update TaggedClaim
calculateClaimsFromFloatingSwapStream s periodicSchedule streamSchedule useAdjustedDatesForDcf issuerPaysLeg currency issuer calendarDataAgency = do
  let
    f = fromSome s.calculationPeriodAmount.calculation.floatingRateCalculation
    resetDates = fromSome s.resetDates
  fixingCalendars <- getHolidayCalendars resetDates.fixingDates.businessCenters issuer calendarDataAgency
  createFloatingRatePaymentClaimsFpml streamSchedule periodicSchedule useAdjustedDatesForDcf f.spreadSchedule.initialValue issuerPaysLeg s.calculationPeriodAmount.calculation.dayCountFraction currency f.floatingRateIndex (merge fixingCalendars) s

-- | HIDE
-- Type-class constraint verified by the bond templates.
type IsBond t =
  ( HasToInterface t Disclosure.I
  , HasToInterface t Instrument.I
  , HasToInterface t Claim.I
  , HasToInterface t Lifecycle.I
  , HasField "id" t Id
  , HasField "version" t Text
  , HasField "issuer" t Party
  , HasField "lastEventTimestamp" t Time
  , HasCreate t
  )

-- | Maps a `Date` to `Time` using the rule in the `DateClock`.
-- From the Daml.Finance.Instrument.Generics.Test file, but could not import here (duplicated for now).
-- In the termsheet only date is mentioned, but lifecycle logic is based on time.
dateToDateClockTime : Date -> Time
dateToDateClockTime = toUTCTime . Unit

-- | Maps a `Date` claim to a `Time` claim using the rule in the `DateClock`.
-- From the Daml.Finance.Instrument.Generics.Test file, but could not import here (duplicated for now).
-- In the termsheet only date is mentioned, but lifecycle logic is based on time.
mapClaimToUTCTime : Claim Date Decimal Deliverable Observable -> C
mapClaimToUTCTime =
  let dateToTime = toUTCTime . Unit
  in toTime' dateToTime

-- BOND_PROCESS_CLOCK_UPDATE_INITAL_CLAIMS_BEGIN
-- | Rule to process a clock update event.
processClockUpdate : IsBond t => Parties -> ContractId Event.I -> ContractId Clock.I -> ContractId Lifecycle.I -> t -> [ContractId Observable.I] -> Update (ContractId Lifecycle.I, [ContractId Effect.I])
processClockUpdate settlers eventCid _ self instrument observableCids = do
  v <- view <$> fetch eventCid
  let
    claimInstrument = toInterface @Claim.I instrument
    acquisitionTime = Claim.getAcquisitionTime claimInstrument
  -- Recover claims tree as of the lastEventTimestamp. For a bond, this just requires lifecycling as of the lastEventTimestamp.
  -- BOND_PROCESS_CLOCK_UPDATE_INITAL_CLAIMS_END
  -- BOND_PROCESS_CLOCK_UPDATE_LIFECYCLE_FASTFORWARD_BEGIN
  claims <- Prelude.fst <$> lifecycle observableCids claimInstrument [timeEvent instrument.lastEventTimestamp]
  -- BOND_PROCESS_CLOCK_UPDATE_LIFECYCLE_FASTFORWARD_END

  -- BOND_PROCESS_CLOCK_UPDATE_LIFECYCLE_BEGIN
  -- Lifecycle
  (remaining, pending) <- lifecycleClaims observableCids acquisitionTime claims [timeEvent v.eventTime]

  let
    nettedPending = net pending
    (consumed, produced) = splitPending nettedPending
  if remaining == claims && null nettedPending then
    pure (self, [])
  else do
    let
      currentKey = Instrument.getKey $ toInterface @Instrument.I instrument
      settlementDate = toDateUTC v.eventTime -- TODO remove this dependency
      newKey = currentKey with version = sha256 $ show remaining
    newInstrumentCid <- create instrument with lastEventTimestamp = v.eventTime; version = newKey.version
    Instrument.createReference instrument.issuer $ toInterfaceContractId newInstrumentCid
    effectCid <- toInterfaceContractId <$> create Effect with
      providers = fromList [currentKey.issuer, currentKey.depository]
      settlers
      id = v.id
      description = v.description
      targetInstrument = currentKey
      producedInstrument = if isZero' remaining then None else Some newKey
      consumed
      produced
      settlementDate
      observers = Disclosure.flattenObservers . (.observers) . view $ toInterface @Disclosure.I instrument
    pure (toInterfaceContractId newInstrumentCid, [effectCid])
  -- BOND_PROCESS_CLOCK_UPDATE_LIFECYCLE_END
