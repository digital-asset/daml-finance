-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Swap.InterestRate.Instrument where

import DA.Date (daysSinceEpochToDate)
import DA.Set (singleton)
import Daml.Finance.Claims.Util.Builders (createFixRatePaymentClaims, createFloatingRatePaymentClaims)
import Daml.Finance.Data.Reference.HolidayCalendar (getHolidayCalendars, rollSchedule)
import Daml.Finance.Data.Time.DateClock (dateToDateClockTime)
import Daml.Finance.Interface.Claims.Claim qualified as Claim (GetClaims(..), I, View(..))
import Daml.Finance.Interface.Claims.Dynamic.Instrument qualified as DynamicInstrument (CreateNewVersion(..), I, View(..))
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as BaseInstrument (I, View(..), createReference, disclosureUpdateReference)
import Daml.Finance.Interface.Instrument.Swap.InterestRate.Instrument qualified as InterestRate (I, View(..))
import Daml.Finance.Interface.Instrument.Swap.InterestRate.Types (InterestRate(..))
import Daml.Finance.Interface.Instrument.Swap.Types (FloatingRate)
import Daml.Finance.Interface.Types.Common.Types (Id(..), InstrumentKey(..), PartiesMap)
import Daml.Finance.Interface.Types.Date.DayCount (DayCountConventionEnum)
import Daml.Finance.Interface.Types.Date.Schedule (PeriodicSchedule(..))
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, View(..), flattenObservers)
import Daml.Finance.Util.Disclosure (addObserversImpl, removeObserversImpl, setObserversImpl)

-- | Type synonym for `Instrument`.
type T = Instrument

-- | This template models an interest rate swap.
-- It pays a fix vs floating rate at the end of every payment period.
-- The floating leg depends on a reference rate (observed at the beginning of the swap payment
-- period). For example: 3M Euribor vs 2.5% fix.
template Instrument
  with
    depository : Party
      -- ^ The depository of the instrument.
    issuer : Party
      -- ^ The issuer of the instrument.
    id : Id
      -- ^ An identifier of the instrument.
    version : Text
      -- ^ The instrument's version.
    description : Text
      -- ^ A description of the instrument.
    floatingRate : FloatingRate
      -- ^ A description of the floating rate to be used. This supports both Libor and SOFR style
      --   reference rates.
    ownerReceivesFix : Bool
      -- ^ Indicate whether a holding owner of this instrument receives the fix or the floating leg
      --   of the swap.
    fixRate : Decimal
      -- ^ The interest rate of the fix leg. For example, in case of "3M Euribor vs 2.5% fix" this
      --   should be 0.025.
    periodicSchedule : PeriodicSchedule
      -- ^ The schedule for the periodic swap payments.
    holidayCalendarIds : [Text]
      -- ^ The identifiers of the holiday calendars to be used for the swap payment schedule.
    calendarDataProvider : Party
      -- ^ The reference data provider to use for the holiday calendar.
    dayCountConvention : DayCountConventionEnum
      -- ^ The day count convention used to calculate day count fractions. For example: Act360.
    currency : InstrumentKey
      -- ^ The currency of the swap. For example, if the swap pays in USD this should be a USD cash
      --   instrument.
    observers : PartiesMap
      -- ^ The observers of the instrument.
    lastEventTimestamp : Time
      -- ^ (Market) time of the last recorded lifecycle event. If no event has occurred yet, the
      --   time of creation should be used.
  where
    signatory depository, issuer
    observer Disclosure.flattenObservers observers

    interface instance Claim.I for Instrument where
      view = Claim.View with acquisitionTime = dateToDateClockTime $ daysSinceEpochToDate 0
      getClaims Claim.GetClaims{actor} = do
        -- get the initial claims tree (as of the swap's acquisition time)
        let getCalendars = getHolidayCalendars actor calendarDataProvider
        schedule <- fst <$> rollSchedule getCalendars periodicSchedule holidayCalendarIds
        let
          useAdjustedDatesForDcf = True
          notional = 1.0
          ownerReceivesFloatingLeg = not ownerReceivesFix
          fixClaims = createFixRatePaymentClaims dateToDateClockTime schedule periodicSchedule
            useAdjustedDatesForDcf fixRate ownerReceivesFix dayCountConvention notional currency
          floatingClaims = createFloatingRatePaymentClaims dateToDateClockTime schedule
            periodicSchedule useAdjustedDatesForDcf 0.0 ownerReceivesFloatingLeg dayCountConvention
            notional currency floatingRate.referenceRateId
        pure [fixClaims, floatingClaims]

    interface instance BaseInstrument.I for Instrument where
      view = BaseInstrument.View with
        depository; issuer; id; version; description; validAsOf = lastEventTimestamp
      getKey = instrumentKey this

    interface instance InterestRate.I for Instrument where
      view = InterestRate.View with
        interestRate = InterestRate with
          instrument = instrumentKey this
          description
          floatingRate
          ownerReceivesFix
          fixRate
          periodicSchedule
          holidayCalendarIds
          calendarDataProvider
          dayCountConvention
          currency
          lastEventTimestamp

    interface instance DynamicInstrument.I for Instrument where
      view = DynamicInstrument.View with lifecycler = issuer; lastEventTimestamp; prevEvents = []
      createNewVersion DynamicInstrument.CreateNewVersion{version; lastEventTimestamp} = do
        cid <- create this with version; lastEventTimestamp
        BaseInstrument.createReference issuer $ toInterfaceContractId cid
        pure $ toInterfaceContractId cid

    interface instance Disclosure.I for Instrument where
      view = Disclosure.View with disclosureControllers = singleton issuer; observers
      setObservers = setObserversImpl this $
        Some (BaseInstrument.disclosureUpdateReference (instrumentKey this))
      addObservers = addObserversImpl @Instrument this $
        Some (BaseInstrument.disclosureUpdateReference (instrumentKey this))
      removeObservers = removeObserversImpl this $
        Some (BaseInstrument.disclosureUpdateReference (instrumentKey this))

-- | HIDE
instrumentKey : Instrument -> InstrumentKey
instrumentKey Instrument {depository; issuer; id; version} =
  InstrumentKey with depository; issuer; id; version
