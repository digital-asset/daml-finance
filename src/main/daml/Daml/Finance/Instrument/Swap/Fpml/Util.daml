-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Swap.Fpml.Util where

import ContingentClaims.Core.Claim (Inequality(..), give, one, scale, when)
import ContingentClaims.Core.Observation (Observation(..))
import DA.Date
import DA.Foldable (foldMap)
import DA.List (head, last)
import DA.Optional (fromOptional, fromSome, isNone, isSome)
import Daml.Finance.Instrument.Generic.Util (createFixRatePaymentClaims, getHolidayCalendars, prepareAndTagClaims)
import Daml.Finance.Interface.Claims.Types (Deliverable, Observable, TaggedClaim(..))
import Daml.Finance.Interface.Types.Date.Calendar
import Daml.Finance.Interface.Types.Date.DayCount (DayCountConventionEnum)
import Daml.Finance.Interface.Types.Date.RollConvention
import Daml.Finance.Interface.Types.Date.Schedule (Frequency(..), PeriodicSchedule(..), SchedulePeriod, StubPeriodTypeEnum(..))
import Daml.Finance.Interface.Types.Fpml
import Daml.Finance.Util.Date.Calendar (addBusinessDays, adjustDate, merge)
import Daml.Finance.Util.Date.DayCount (calcPeriodDcf)
import Daml.Finance.Util.Date.RollConvention (addPeriod)
import Prelude hiding (key)

type O = Observation Date Decimal Observable

-- | Create a schedule for calculation periods.
createCalculationPeriodicSchedule : CalculationPeriodDates -> PeriodicSchedule
createCalculationPeriodicSchedule c = do
  let
    periodicSchedule = PeriodicSchedule with
      businessDayAdjustment =
        BusinessDayAdjustment with
          calendarIds = c.calculationPeriodDatesAdjustments.businessCenters
          convention = c.calculationPeriodDatesAdjustments.businessDayConvention
      effectiveDateBusinessDayAdjustment = None
      terminationDateBusinessDayAdjustment = None
      frequency =
        Frequency with
          rollConvention = c.calculationPeriodFrequency.rollConvention
          period = c.calculationPeriodFrequency.period
          periodMultiplier = c.calculationPeriodFrequency.periodMultiplier
      effectiveDate = c.effectiveDate.unadjustedDate
      firstRegularPeriodStartDate = c.firstRegularPeriodStartDate
      lastRegularPeriodEndDate = c.lastRegularPeriodEndDate
      stubPeriodType = None
      terminationDate = c.terminationDate.unadjustedDate
  periodicSchedule

-- | Create a schedule for payment periods.
createPaymentPeriodicSchedule : SwapStream -> PeriodicSchedule
createPaymentPeriodicSchedule s = do
  let
    periodicSchedule = PeriodicSchedule with
      businessDayAdjustment =
        BusinessDayAdjustment with
          calendarIds = fromSome s.paymentDates.paymentDatesAdjustments.businessCenters
          convention = s.paymentDates.paymentDatesAdjustments.businessDayConvention
      effectiveDateBusinessDayAdjustment = None
      terminationDateBusinessDayAdjustment = None
      frequency =
        Frequency with
          rollConvention = s.calculationPeriodDates.calculationPeriodFrequency.rollConvention
          period = s.paymentDates.paymentFrequency.period
          periodMultiplier = s.paymentDates.paymentFrequency.periodMultiplier
      effectiveDate = s.calculationPeriodDates.effectiveDate.unadjustedDate
      firstRegularPeriodStartDate = s.paymentDates.firstPaymentDate
      lastRegularPeriodEndDate = s.paymentDates.lastRegularPaymentDate
      stubPeriodType = None
      terminationDate = s.calculationPeriodDates.terminationDate.unadjustedDate
  periodicSchedule

-- | Retrieve holiday calendars and adjust a date as specified in a BusinessDayAdjustments FpML element
getCalendarsAndAdjust : Date -> BusinessDayAdjustments -> Party -> Party -> Update Date
getCalendarsAndAdjust unadjustedDate businessDayAdjustments issuer calendarDataAgency = do
  let holidayCalendarIds = fromSome businessDayAdjustments.businessCenters
  cals <- getHolidayCalendars holidayCalendarIds issuer calendarDataAgency
  pure $ adjustDate (merge cals) businessDayAdjustments.businessDayConvention unadjustedDate

-- | Adjust a date as specified in a BusinessDayAdjustments FpML element (or not at all if NoAdjustment)
adjustDateAccordingToBusinessDayAdjustments : Date -> BusinessDayAdjustments -> Party -> Party -> Update Date
adjustDateAccordingToBusinessDayAdjustments unadjustedDate businessDayAdjustments issuer calendarDataAgency = do
  case businessDayAdjustments.businessDayConvention of
    NoAdjustment -> pure $ unadjustedDate
    _ -> getCalendarsAndAdjust unadjustedDate businessDayAdjustments issuer calendarDataAgency

-- | Create claims from swapStream that describes a fix coupon stream.
calculateClaimsFromFixSwapStream : SwapStream -> PeriodicSchedule -> [SchedulePeriod] -> [SchedulePeriod] -> Bool -> Bool -> Decimal -> Deliverable -> Update TaggedClaim
calculateClaimsFromFixSwapStream s periodicSchedule calculationSchedule paymentSchedule useAdjustedDatesForDcf issuerPaysLeg notional currency = do
  assertMsg "The payment schedule must match the calculation schedule" $ paymentSchedule == calculationSchedule
  assertMsg "Non-standard stub rates not supported for the fixed rate leg" $ isNone s.stubCalculationPeriodAmount
  let f = fromSome s.calculationPeriodAmount.calculation.fixedRateSchedule
  pure $ createFixRatePaymentClaims calculationSchedule periodicSchedule useAdjustedDatesForDcf f.initialValue issuerPaysLeg s.calculationPeriodAmount.calculation.dayCountFraction notional currency

-- | Define observable part of claim when one specific floating rate is provided for a stub period.
getSingleStubRate : StubFloatingRate -> Optional O
getSingleStubRate floatingRate = do
  Some (Observe floatingRate.floatingRateIndex)

-- | Linearly interpolates two rates within a period, as specified in https://www.isda.org/a/aWkgE/Linear-interpolation-04022022.pdf
getInterpolatedStubRate : StubFloatingRate -> StubFloatingRate -> SchedulePeriod -> HolidayCalendarData -> BusinessDayConventionEnum -> Optional O
getInterpolatedStubRate fr1 fr2 p cal convention = do
  let
    tenor1 = fromSome fr1.indexTenor
    tenor2 = fromSome fr2.indexTenor
    s = p.adjustedStartDate
    p1 = adjustDate cal convention (addPeriod s tenor1)
    p2 = adjustDate cal convention (addPeriod s tenor2)
    tn = subDate p.adjustedEndDate s
    t1 = subDate p1 s
    t2 = subDate p2 s
    d1 = tn - t1
    d2 = t2 - tn
    w1 = intToDecimal d2 / intToDecimal (d1 + d2)
    w2 = 1.0 - w1

  -- In the ISDA paper, the following expression is used for linear interpolation:
  --Some (Observe (fr1.floatingRateIndex) + (Observe (fr2.floatingRateIndex) - Observe (fr1.floatingRateIndex)) * Const (intToDecimal (tn - t1) / intToDecimal (t2 - t1)))

  -- This expression can be simplified to:
  Some $ Const w1 * Observe fr1.floatingRateIndex + Const w2 * Observe fr2.floatingRateIndex

-- | Get the floating stub rate to be used for a stub period.
getStubRateFloating : [StubFloatingRate] -> SchedulePeriod -> HolidayCalendarData -> BusinessDayConventionEnum -> Optional O
getStubRateFloating floatingRates p cal convention = do
  case floatingRates of
    [rate] -> getSingleStubRate rate
    [rate1, rate2] -> getInterpolatedStubRate rate1 rate2 p cal convention
    other -> error "only one or two floating stub rates supported"

-- | Get the stub rate to be used for a stub period.
-- Currently, three main options from the FpML schema are supported:
-- 1. A fix stubRate.
-- 2. One or two floating rates for the stub.
-- 3. No specific stub rate defined -> use the same rate as is used for regular periods.
getStubRate : StubCalculationPeriodAmount -> Bool -> SchedulePeriod -> HolidayCalendarData -> BusinessDayConventionEnum -> Optional O
getStubRate sc initialStub p cal convention = do
  let
    s = if initialStub then fromSome sc.initialStub else fromSome sc.finalStub
  case s.stubRate of
    Some stubRate -> if isSome s.floatingRate then error "both stubRate and floatingRate provided" else Const <$> s.stubRate
    None -> if isSome s.floatingRate then getStubRateFloating (fromSome s.floatingRate) p cal convention else None

-- | Align the payment schedule with the calculation schedule.
alignPaymentSchedule : [SchedulePeriod] -> [SchedulePeriod] -> Update [SchedulePeriod]
alignPaymentSchedule calculationSchedule paymentSchedule = do
  assertMsg "The payment schedule has fewer periods than the calculation schedule" $ length paymentSchedule <= length calculationSchedule
  let
    calculationScheduleWithoutStubs = filter (isNone . (.stubType)) calculationSchedule
    paymentScheduleWithoutStubs = filter (isNone . (.stubType)) paymentSchedule
  assertMsg "The payment schedule periods must be an even multiple of the calculation schedule periods" $ length calculationScheduleWithoutStubs % length paymentScheduleWithoutStubs == 0
  let
    nCalculationPeriodsPerPaymentPeriod = length calculationScheduleWithoutStubs / length paymentScheduleWithoutStubs
    paymentScheduleAlignedWithoutStubs = foldMap (replicate nCalculationPeriodsPerPaymentPeriod) paymentScheduleWithoutStubs
    firstPeriod = head paymentSchedule
    lastPeriod = last paymentSchedule
    initialStubIfItExists = case firstPeriod.stubType of
      Some ShortInitial -> [firstPeriod]
      Some LongInitial -> [firstPeriod]
      _ -> []
    finalStubIfItExists = case lastPeriod.stubType of
      Some ShortFinal -> [lastPeriod]
      Some LongFinal -> [lastPeriod]
      _ -> []
    paymentScheduleAligned = initialStubIfItExists ++ paymentScheduleAlignedWithoutStubs ++ finalStubIfItExists
  assertMsg "The payment schedule must match the calculation schedule" $ length paymentScheduleAligned == length calculationSchedule
  pure paymentScheduleAligned

-- | Calculate a floating rate amount for each payment date and create claims.
-- The floating rate is observed according to the FpML ResetDates component and used for the corresponding payment on the last day of that payment period.
createFloatingRatePaymentClaims : [SchedulePeriod] -> [SchedulePeriod] -> PeriodicSchedule -> Bool -> Decimal -> Bool -> DayCountConventionEnum -> Decimal -> Deliverable -> Observable -> HolidayCalendarData -> SwapStream -> Update TaggedClaim
createFloatingRatePaymentClaims calculationSchedule paymentSchedule periodicSchedule useAdjustedDatesForDcf floatingRateSpread issuerPays dayCountConvention notional cashInstrumentCid referenceRateId fixingCalendars s = do
  let
    resetDates = fromSome s.resetDates
    bdc = s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessDayConvention
  paymentScheduleAligned <- alignPaymentSchedule calculationSchedule paymentSchedule
  assertMsg "Only daily fixing date period supported" $ resetDates.fixingDates.period == D
  assertMsg "Only Business day type supported" $ resetDates.fixingDates.dayType == Business
  assertMsg "Only fixing dates relative to calculation start dates supported" $ resetDates.resetRelativeTo == CalculationPeriodStartDate
  assertMsg "No adjustement (besides business day type) supported" $ resetDates.fixingDates.businessDayConvention == NoAdjustment
  let
    fixingDates = map (\p -> addBusinessDays fixingCalendars resetDates.fixingDates.periodMultiplier p.adjustedStartDate) calculationSchedule
    createClaim c p f =
      when (TimeGte f) $ scale (rate * (Const (calcPeriodDcf dayCountConvention c useAdjustedDatesForDcf periodicSchedule.terminationDate periodicSchedule.frequency))) $
      when (TimeGte p.adjustedEndDate) $ scale (Const notional) $ one cashInstrumentCid
        where
          regularRate = Observe referenceRateId + Const floatingRateSpread
          rate = case c.stubType of
            None -> regularRate
            Some stubType -> if isNone s.stubCalculationPeriodAmount then regularRate else fromOptional regularRate (getStubRate (fromSome s.stubCalculationPeriodAmount) (stubType == LongInitial || stubType == ShortInitial) p fixingCalendars bdc)
    floatingClaimAmounts = mconcat $ zipWith3 createClaim calculationSchedule paymentScheduleAligned fixingDates
    floatingClaims = if issuerPays then floatingClaimAmounts else give floatingClaimAmounts
  pure $ prepareAndTagClaims [floatingClaims] "Floating rate payment"

-- | Create claims from swapStream that describes a floating coupon stream.
calculateClaimsFromFloatingSwapStream : SwapStream -> PeriodicSchedule -> [SchedulePeriod] -> [SchedulePeriod] -> Bool -> Bool -> Decimal -> Deliverable -> Party -> Party -> Update TaggedClaim
calculateClaimsFromFloatingSwapStream s periodicSchedule calculationSchedule paymentSchedule useAdjustedDatesForDcf issuerPaysLeg notional currency issuer calendarDataAgency= do
  let
    f = fromSome s.calculationPeriodAmount.calculation.floatingRateCalculation
    r = fromSome s.resetDates
  assertMsg "stub must refer to the calculationPeriodDates of the current swapStream" $ isNone s.stubCalculationPeriodAmount || getField @"calculationPeriodDatesReference" (fromSome s.stubCalculationPeriodAmount) == s.calculationPeriodDates.id
  assertMsg "resetDate must refer to the calculationPeriodDates of the current swapStream" $ r.calculationPeriodDatesReference == s.calculationPeriodDates.id
  assertMsg "resetDate period must match calculation period" $ r.resetFrequency.period == s.calculationPeriodDates.calculationPeriodFrequency.period
  assertMsg "resetDate periodMultiplier must match calculation periodMultiplier" $ r.resetFrequency.periodMultiplier == s.calculationPeriodDates.calculationPeriodFrequency.periodMultiplier
  assertMsg "resetDate businessDayConvention must match calculation businessDayConvention" $ r.resetDatesAdjustments.businessDayConvention == s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessDayConvention
  assertMsg "resetDate businessCenters must match calculation businessCenters" $ r.resetDatesAdjustments.businessCenters == s.calculationPeriodDates.calculationPeriodDatesAdjustments.businessCenters
  fixingCalendars <- getHolidayCalendars r.fixingDates.businessCenters issuer calendarDataAgency
  createFloatingRatePaymentClaims calculationSchedule paymentSchedule periodicSchedule useAdjustedDatesForDcf f.spreadSchedule.initialValue issuerPaysLeg s.calculationPeriodAmount.calculation.dayCountFraction notional currency f.floatingRateIndex (merge fixingCalendars) s
