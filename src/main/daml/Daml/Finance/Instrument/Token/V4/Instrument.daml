-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Token.V4.Instrument where

import DA.Set (singleton)
import Daml.Finance.Interface.Instrument.Base.V4.Instrument qualified as BaseInstrument (I, View(..), disclosureUpdateReference, instrumentKey)
import Daml.Finance.Interface.Instrument.Token.V4.Instrument qualified as Token (I, View(..))
import Daml.Finance.Interface.Instrument.Token.V4.Types (Token(..))
import Daml.Finance.Interface.Types.Common.V3.Types (HoldingStandard(..), Id, PartiesMap)
import Daml.Finance.Interface.Util.V3.Disclosure qualified as Disclosure (I, View(..), flattenObservers)
import Daml.Finance.Util.V4.Disclosure (addObserversImpl, removeObserversImpl, setObserversImpl)

-- | Type synonym for `Instrument`.
type T = Instrument

-- | Implementation of a Token Instrument, which is a simple instrument whose economic terms
-- on the ledger are represented by an `id` and a textual `description`.
template Instrument
  with
    depository : Party
      -- ^ The instrument's depository.
    issuer : Party
      -- ^ The instrument's issuer.
    id : Id
      -- ^ The intrument's identifier.
    version : Text
      -- ^ A textual instrument version.
    holdingStandard : HoldingStandard
      -- ^ The holding standard for holdings referencing this instrument.
    description : Text
      -- ^ A description of the instrument.
    validAsOf : Time
      -- ^ Timestamp as of which the instrument is valid. This usually coincides with the timestamp
      --   of the event that creates the instrument. It usually does not coincide with ledger time.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory depository, issuer
    observer Disclosure.flattenObservers observers

    interface instance BaseInstrument.I for Instrument where
      view = BaseInstrument.View with
        depository; id; issuer; version; holdingStandard; description; validAsOf
      getKey = BaseInstrument.instrumentKey this

    interface instance Token.I for Instrument where
      view = Token.View with
        token = Token with instrument = BaseInstrument.instrumentKey this; description; validAsOf

    interface instance Disclosure.I for Instrument where
      view = Disclosure.View with disclosureControllers = singleton issuer; observers
      setObservers = setObserversImpl this . Some . BaseInstrument.disclosureUpdateReference $
        BaseInstrument.instrumentKey this
      addObservers = addObserversImpl this . Some . BaseInstrument.disclosureUpdateReference $
        BaseInstrument.instrumentKey this
      removeObservers = removeObserversImpl this . Some . BaseInstrument.disclosureUpdateReference $
        BaseInstrument.instrumentKey this
