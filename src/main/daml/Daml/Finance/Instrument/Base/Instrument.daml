-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Base.Instrument where

import DA.Set (singleton)
import Daml.Finance.Interface.Instrument.Base.Factory qualified as InstrumentFactory (Create(..), F, HasImplementation, Remove(..), View(..))
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (GetCid(..), HasImplementation, I, R, View(..), createReference, disclosureUpdateReference)
import Daml.Finance.Interface.Types.Common (Id, InstrumentKey(..), Observers)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, SetObservers(..), View(..), flattenObservers)

-- | Type synonym for `Instrument`.
type T = Instrument

instance Instrument.HasImplementation T
-- | Base implementation of an Instrument which does not define any lifecycling logic.
template Instrument
  with
    depository : Party
      -- ^ The instrument's depository.
    issuer : Party
      -- ^ The instrument's issuer.
    id : Id
      -- ^ The intrument's versioned identifier.
    version : Text
      -- ^ A textual instrument version.
    description : Text
      -- ^ A description of the instrument.
    observers : Observers
      -- ^ Observers.
    validAsOf : Time
      -- ^ Timestamp as of which the instrument is valid. This usually coincides with the timestamp of the event that creates the instrument. It usually does not coincide with ledger time.
  where
    signatory depository, issuer
    observer Disclosure.flattenObservers observers

    let instrumentKey = InstrumentKey with depository; issuer; id; version

    interface instance Instrument.I for Instrument where
      asDisclosure = toInterface @Disclosure.I this
      view = Instrument.View with depository; issuer; id; validAsOf; description; version
      getKey = instrumentKey

    interface instance Disclosure.I for Instrument where
      view = Disclosure.View with disclosureControllers = singleton $ singleton issuer; observers
      setObservers Disclosure.SetObservers{newObservers} = do
        cid <- toInterfaceContractId <$> create this with observers = newObservers
        Instrument.disclosureUpdateReference newObservers instrumentKey cid
      archive' self = archive (coerceContractId self : ContractId Instrument)

instance InstrumentFactory.HasImplementation Factory
-- | Factory template for instrument creation.
template Factory
  with
    provider : Party
      -- ^ The factory's provider.
    observers : Observers
      -- ^ The factory's observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance InstrumentFactory.F for Factory where
      asDisclosure = toInterface @Disclosure.I this
      view = InstrumentFactory.View with provider
      create' InstrumentFactory.Create{instrument; validAsOf; observers; description} = do
        cid <- toInterfaceContractId <$>
          create Instrument with depository = instrument.depository; issuer = instrument.issuer; id = instrument.id; version = instrument.version; description; validAsOf; observers
        Instrument.createReference instrument.depository cid
        pure cid
      remove InstrumentFactory.Remove{instrument} = do
        (refCid, ref) <- fetchByKey @Instrument.R instrument
        instrumentCid <- exercise refCid Instrument.GetCid with viewer = instrument.depository
        archive $ fromInterfaceContractId @Instrument instrumentCid
        archive refCid

    interface instance Disclosure.I for Factory where
      view = Disclosure.View with disclosureControllers = singleton $ singleton provider; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId Instrument)
