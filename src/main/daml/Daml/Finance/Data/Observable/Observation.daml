-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Data.Observable.Observation
  ( Observation(..)
  ) where

import DA.Map as M (Map, lookup)
import DA.Set (singleton)
import Daml.Finance.Interface.Data.NumericObservable qualified as NumericObservable (HasImplementation, I, View(..))
import Daml.Finance.Interface.Types.Common (Id, PartiesMap)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, SetObservers(..), View(..), flattenObservers)

-- | Type synonym for `Observation`.
type T = Observation

instance NumericObservable.HasImplementation T

-- | An implementation of `NumericObservable` that explicitly stores time-dependent numerical
-- values. For example, it can be used for equity or rate fixings.
template Observation
  with
    provider : Party
      -- ^ The reference data provider.
    id : Id
      -- ^ A textual identifier.
    observations : Map Time Decimal
      -- ^ The time-dependent values.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance NumericObservable.I for Observation where
      view = NumericObservable.View with provider; id; observations
      observe t =
        case M.lookup t observations of
          Some obs -> pure obs
          None -> fail $ "Missing observation for " <> show id <>" at t = " <> show t

    interface instance Disclosure.I for Observation where
      view = Disclosure.View with disclosureControllers = singleton provider; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$>
        create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId Observation)
