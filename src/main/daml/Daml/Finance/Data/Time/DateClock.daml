-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Data.Time.DateClock where

import DA.Date (addDays)
import DA.Time (time)
import Daml.Finance.Interface.Data.TimeObservable qualified as TimeObservable (HasImplementation, I, View(..))
import Daml.Finance.Interface.Types.Common.Types (Id, Parties)
import Daml.Finance.Interface.Types.Date.Classes (HasUTCTimeConversion(..))
import Daml.Finance.Data.Time.DateClock.Types (Unit(..))
import Daml.Finance.Lifecycle.Event.DateClockUpdate (DateClockUpdateEvent(..))

-- | Type synonym for `DateClock`.
type T = DateClock

instance TimeObservable.HasImplementation DateClock

-- | A `DateClock` is a template used to keep track of the current date.
-- The `ToNext` choice moves the clock to the next date and spawns a `DateClockUpdateEvent`.
-- The `DateClock` implements the `TimeObservable` interface. Specifically, each date `D` is mapped
-- to `D 12:00:00 UTC`. If your use-case involves working across multiple time zones, you may
-- need to define multiple `DateClock` templates with specific time conversions.
template DateClock
  with
    providers : Parties
      -- ^ The clock's providers.
    date : Unit
      -- ^ The clock's date.
    id : Id
      -- ^ The clock's identifier.
    description : Text
      -- ^ The clock's description.
    observers : Parties
      -- ^ Observers.
  where
    signatory providers
    observer observers

    key (providers, id) : (Parties, Id)
    maintainer key._1

    interface instance TimeObservable.I for DateClock where
      view = TimeObservable.View with providers; id
      getTime =
        let Unit currentDate = date
        in pure $ time currentDate 12 0 0

    choice ToNext : (ContractId DateClock, ContractId DateClockUpdateEvent)
      -- ^ Moves the clock to the next date and spawns an update event.
      with
        eventId : Id
          -- ^ Event identifier.
        eventDescription : Text
          -- ^ Event description.
      controller providers
      do
        let
          Unit currentDate = date
          clock = this with date = Unit $ addDays currentDate 1
        clockCid <- create clock
        eventCid <- create DateClockUpdateEvent with
          providers
          observers
          id = eventId
          description = eventDescription
          eventTime = toUTCTime this
          date = currentDate
        pure (clockCid, eventCid)

instance HasUTCTimeConversion DateClock where
  toUTCTime clock = toUTCTime clock.date

instance Ord DateClock where
  compare x y = compare x.date y.date
