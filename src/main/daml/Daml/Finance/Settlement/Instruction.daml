-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Settlement.Instruction where

import DA.Assert ((===))
import DA.Optional (fromSome)
import DA.Set (fromList, insert, singleton)
import Daml.Finance.Interface.Common.Disclosure qualified as Disclosure (AddObservers(..), SetObservers(..), I, View(..))
import Daml.Finance.Interface.Common.Types (AccountKey(..), Id, Observers, Parties)
import Daml.Finance.Interface.Common.Util (flattenObservers)
import Daml.Finance.Interface.Holding.Account qualified as Account (exerciseInterfaceByKey)
import Daml.Finance.Interface.Holding.Transferable qualified as Transferable (I, Transfer(..))
import Daml.Finance.Interface.Holding.Util (getAccount, getAmount, getInstrument)
import Daml.Finance.Interface.Settlement.Instruction qualified as Instruction (Allocate(..), Approve(..), HasImplementation, I, View(..))
import Daml.Finance.Interface.Settlement.Types (Step)

-- | Type synonym for `Instruction`.
type T = Instruction

instance Instruction.HasImplementation T

-- | Instruction is used to settle a single settlement `Step`. In order to settle the instruction,
-- - the sender must allocate a suitable transferable holding.
-- - the receiver must define the receiving account.
template Instruction
  with
    requestors : Parties
      -- ^ Parties requesting the settlement.
    signed : Parties
      -- ^ Additional signatories, used to collect authorization.
    settler : Party
      -- ^ Party triggering the settlement.
    step : Step
      -- ^ Settlement step.
    allocation : Optional (ContractId Transferable.I)
      -- ^ Allocated holding.
    account : Optional AccountKey
      -- ^ Receiving account.
    id : Id
      -- ^ Instruction identifier.
    observers : Observers
      -- ^ Observers.
  where
    signatory requestors, signed
    observer step.sender, step.receiver, settler, flattenObservers observers

    key (requestors, id) : (Parties, Id)
    maintainer fst key

    implements Disclosure.I where
      view = Disclosure.View with disclosureControllers = fromList [singleton step.sender, singleton step.receiver]; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId Instruction)

    implements Instruction.I where
      asDisclosure = toInterface @Disclosure.I this
      view = Instruction.View with step; settler; id
      allocate Instruction.Allocate{transferableCid} = do
        transferableCid <- coerceContractId <$> exercise (toInterfaceContractId @Disclosure.I transferableCid) Disclosure.AddObservers with disclosers = singleton step.sender; observersToAdd = (id.label, singleton settler)
        transferable <- fetch transferableCid
        accountCid <- Account.exerciseInterfaceByKey @Disclosure.I (getAccount transferable) step.sender Disclosure.AddObservers with disclosers = singleton step.sender; observersToAdd = (id.label, singleton settler)
        getAmount transferable === step.quantity.amount
        getInstrument transferable === step.quantity.unit
        toInterfaceContractId <$> create this with allocation = Some transferableCid; signed = insert step.sender signed
      approve Instruction.Approve{receiverAccount} = do
        Account.exerciseInterfaceByKey @Disclosure.I receiverAccount step.receiver Disclosure.AddObservers with disclosers = singleton step.receiver; observersToAdd = (id.label, singleton settler)
        toInterfaceContractId <$> create this with signed = insert step.receiver signed; account = Some receiverAccount
      execute = exercise (fromSome allocation) Transferable.Transfer with newOwnerAccount = fromSome account
