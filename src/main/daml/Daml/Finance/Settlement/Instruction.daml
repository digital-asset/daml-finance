-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Settlement.Instruction where

import DA.Action (when)
import DA.Assert ((===))
import DA.Functor (($>))
import DA.Set qualified as S (fromList, insert, intersection, isSubsetOf, member, null, singleton)
import DA.Tuple (fst3)
import Daml.Finance.Interface.Holding.Account qualified as Account (I, Credit(..), Debit(..), R, disclose, exerciseInterfaceByKey, undisclose)
import Daml.Finance.Interface.Holding.Lockable (getLocker)
import Daml.Finance.Interface.Holding.Transferable qualified as Transferable (I, Transfer(..))
import Daml.Finance.Interface.Holding.Util (disclose, getAccount, getAmount, getInstrument, undisclose)
import Daml.Finance.Interface.Settlement.Instruction qualified as Instruction (Allocate(..), Approve(..), Execute(..), HasImplementation, I, View(..))
import Daml.Finance.Interface.Settlement.Types (Allocation(..), Approval(..), Step)
import Daml.Finance.Interface.Types.Common (Id(..), Parties, PartiesMap)
import Daml.Finance.Interface.Util.Common (fetchInterfaceByKey)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, SetObservers(..), View(..), flattenObservers)

-- | Type synonym for `Instruction`.
type T = Instruction

instance Instruction.HasImplementation T

-- | Instruction is used to settle a single settlement `Step`. In order to settle the instruction,
-- - the sender must allocate a suitable transferable holding.
-- - the receiver must define the receiving account.
template Instruction
  with
    requestors : Parties
      -- ^ Parties requesting the settlement.
    settlers : Parties
      -- ^ Any of the parties can trigger the settlement.
    signed : Parties
      -- ^ Additional signatories, used to collect authorization.
    batchId : Id
      -- ^ Trade identifier.
    id : Id
      -- ^ Instruction identifier.
    step : Step
      -- ^ Settlement step.
    allocation : Allocation
      -- ^ Allocation from the sender.
    approval : Approval
      -- ^ Approval from the receiver.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory requestors, signed
    observer step.sender, step.receiver, settlers, Disclosure.flattenObservers observers

    key (requestors, batchId, id) : (Parties, Id, Id)
    maintainer fst3 key

    let
      messageSuffix = " / instruction id = " <> show id
      discloseA account = Account.disclose (show batchId, settlers) account.owner (S.singleton account.owner) account
      undiscloseA account = Account.undisclose (show batchId, settlers) account.owner (S.singleton account.owner) account
      discloseT discloser settlers cid = disclose @Transferable.I (show batchId, settlers) discloser (S.singleton discloser) cid
      undiscloseT discloser settlers cid = undisclose @Transferable.I (show batchId, settlers) (S.singleton discloser) cid

    interface instance Disclosure.I for Instruction where
      view = Disclosure.View with disclosureControllers = S.fromList [step.sender, step.receiver]; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId Instruction)

    interface instance Instruction.I for Instruction where
      asDisclosure = toInterface @Disclosure.I this
      view = Instruction.View with requestors; settlers; signed; batchId; id; step; allocation; approval
      allocate Instruction.Allocate{allocation; actors} = do
        assertMsg ("allocation must be new" <> messageSuffix) $ allocation /= this.allocation
        -- clean up if previously allocated
        otCid <- case this.allocation of
          Pledge transferableCid -> do
            transferable <- fetch transferableCid
            undiscloseA $ getAccount transferable
            undiscloseT step.sender settlers transferableCid
          a -> pure None
        -- verify and disclose allocation
        allocationDisclosed <- case allocation of
          Pledge transferableCid -> do
            transferableCid <- discloseT step.sender settlers transferableCid
            transferable <- fetch transferableCid
            let senderAccount = getAccount transferable
            vSenderAccount <- view <$> fetchInterfaceByKey @Account.R senderAccount
            assertMsg "actors must include transferors" $ S.isSubsetOf (view transferable).transferors actors
            assertMsg "actors must include lockers" $ S.isSubsetOf (getLocker transferable) actors
            discloseA senderAccount
            getAmount transferable === step.quantity.amount
            getInstrument transferable === step.quantity.unit
            pure $ Pledge transferableCid
          a -> do
            assertMsg "actors must include sender" $ S.member step.sender actors
            pure a
        -- update instruction state
        cid <- toInterfaceContractId <$> create this with allocation = allocationDisclosed; signed = S.insert step.sender signed
        pure (cid, otCid)
      approve Instruction.Approve{approval; actors} = do
        assertMsg ("approval must be new" <> messageSuffix) $ approval /= this.approval
        -- clean up if previously approved
        case this.approval of
          TakeDelivery receiverAccount -> undiscloseA receiverAccount
          a -> pure None
        -- disclose approval
        case approval of
          TakeDelivery receiverAccount -> do
            vReceiverAccount <- view <$> fetchInterfaceByKey @Account.R receiverAccount
            discloseA receiverAccount
            assertMsg "actors must include transfer approvers" $ S.isSubsetOf vReceiverAccount.transferApprovers actors
          _ ->
            assertMsg "actors must include receiver" $ S.member step.receiver actors
        -- update instruction state
        toInterfaceContractId <$> create this with signed = S.insert step.receiver signed; approval
      execute Instruction.Execute{actors; undiscloseSenderAccount; undiscloseReceiverAccount} = do
        assertMsg "actors must intersect with settlers" $ not $ S.null $ S.intersection actors settlers
        -- execute instruction
        case (allocation, approval) of
          (Unallocated, _) -> abort $ "instruction must be allocated" <> messageSuffix
          (_, Unapproved) -> abort $ "instruction must be approved" <> messageSuffix
          (Pledge transferableCid, TakeDelivery receiverAccount) -> do
            transferable <- fetch transferableCid
            let senderAccount = getAccount transferable
            transferableCid <- exercise transferableCid Transferable.Transfer with newOwnerAccount = receiverAccount; transferApprovers = signed
            when undiscloseSenderAccount $ undiscloseA senderAccount $> ()
            when undiscloseReceiverAccount $ undiscloseA receiverAccount $> ()
            -- disclose to actors (such that they can get the TemplateTypeRep in the Batch)
            Some <$> discloseT step.receiver actors transferableCid
          (Pledge transferableCid, DebitSender) -> do
            transferable <- fetch transferableCid
            let senderAccount = getAccount transferable
            assertMsg ("the custodian of the transferable must be the receiver" <> messageSuffix) $ senderAccount.custodian == step.receiver
            accountCid <- Account.exerciseInterfaceByKey @Account.I senderAccount step.sender Account.Debit with holdingCid = coerceContractId transferableCid
            when undiscloseSenderAccount $ undiscloseA senderAccount $> ()
            pure None
          (CreditReceiver, TakeDelivery receiverAccount) -> do
            assertMsg ("the custodian of the receiver account must be the sender" <> messageSuffix) $ receiverAccount.custodian == step.sender
            transferableCid : ContractId Transferable.I <- coerceContractId <$> Account.exerciseInterfaceByKey @Account.I receiverAccount step.receiver Account.Credit with quantity = step.quantity
            when undiscloseReceiverAccount $ undiscloseA receiverAccount $> ()
            -- disclose to actors (such that they can get the TemplateTypeRep in the Batch)
            Some <$> discloseT step.receiver actors transferableCid
          (CreditReceiver, DebitSender) -> do
            assertMsg ("sender must be the same party as receiver" <> messageSuffix) $ step.sender == step.receiver
            pure None
          (SettleOffLedger, SettleOffLedgerAcknowledge) -> pure None
          (SettleOffLedger, _) -> abort $ "offledger settlement must be acknowledged" <> messageSuffix
          (_, SettleOffLedgerAcknowledge) -> abort $ "offledger settlement must be initiated" <> messageSuffix
