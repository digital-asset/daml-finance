-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Account.Account where

import DA.Map qualified as M (empty)
import DA.Set qualified as S (fromList, null, singleton)
import Daml.Finance.Interface.Account.Account qualified as Account (Controllers(..), Credit(..), Debit(..), I, View(..))
import Daml.Finance.Interface.Account.Factory qualified as AccountFactory (Create(..), F, Remove(..), View(..))
import Daml.Finance.Interface.Holding.Factory qualified as HoldingFactory (Create(..), F, Remove(..))
import Daml.Finance.Interface.Types.Common.Types (AccountKey(..), Id, PartiesMap)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, View(..), flattenObservers)
import Daml.Finance.Interface.Util.KeyTable (archiveByKey, create, exercise, fetch, withKeyTable)
import Daml.Finance.Interface.Util.KeyTable qualified as SyntheticallyKeyed (I, View(..))
import Daml.Finance.Util.Disclosure (addObserversImpl, removeObserversImpl, setObserversImpl)
import Prelude hiding (archive, create, exercise, exerciseByKey, fetch, fetchByKey, fetchFromInterface, lookup, lookupByKey)

-- | Type synonym for `Account`.
type T = Account

-- | A relationship between a custodian and an asset owner. It is referenced by holdings.
template Account
  with
    custodian : Party
      -- ^ The account provider.
    owner : Party
      -- ^ The account owner.
    controllers : Account.Controllers
      -- ^ Controllers of transfers.
    id : Id
      -- ^ Identifier of the account.
    description : Text
      -- ^ Description of the account.
    holdingFactoryCid : ContractId HoldingFactory.F
      -- ^ Associated holding factory.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory custodian, owner
    observer Disclosure.flattenObservers observers

    -- Outgoing controllers must be non-empty.
    ensure not . S.null $ controllers.outgoing

    let account = AccountKey with custodian; owner; id

    interface instance SyntheticallyKeyed.I for Account where
      view = SyntheticallyKeyed.View {}

    interface instance Account.I for Account where
      view = Account.View with custodian; id; owner; holdingFactoryCid; description; controllers
      getKey = account
      credit Account.Credit{quantity} =
        exercise holdingFactoryCid HoldingFactory.Create with
          instrument = quantity.unit
          account
          amount = quantity.amount
          observers = M.empty
      debit Account.Debit{holdingCid} = do
        vHolding <- view <$> fetch holdingCid
        assertMsg "accounts must match" $ vHolding.account == account
        exercise holdingFactoryCid HoldingFactory.Remove with
          actors = S.fromList [account.custodian, account.owner]; holdingCid

    interface instance Disclosure.I for Account where
      view = Disclosure.View with disclosureControllers = S.fromList [custodian, owner]; observers
      setObservers = setObserversImpl @Account this
      addObservers = addObserversImpl @Account this
      removeObservers = removeObserversImpl @Account this

-- | Template used to create accounts.
template Factory
  with
    provider : Party
      -- ^ The factory's provider.
    observers : PartiesMap
      -- ^ The factory's observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance AccountFactory.F for Factory where
      view = AccountFactory.View with provider
      create' AccountFactory.Create {account; holdingFactoryCid; controllers; observers;
        description} =
          toInterfaceContractId <$> create Account with
            custodian = account.custodian; owner = account.owner; controllers; id = account.id
            holdingFactoryCid; observers; description
      remove AccountFactory.Remove{account; keyTable} = withKeyTable keyTable do
        archiveByKey @Account.I @AccountKey provider account

    interface instance Disclosure.I for Factory where
      view = Disclosure.View with disclosureControllers = S.singleton provider; observers
      setObservers = setObserversImpl @Factory this
      addObservers = addObserversImpl @Factory this
      removeObservers = removeObserversImpl @Factory this
