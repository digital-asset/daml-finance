-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Bond.Util where

import ContingentClaims.Claim (Claim(..), Inequality(..), one, scale, when)
import ContingentClaims.Observation (Observation(..))
import DA.Date
import DA.Record (HasField)
import DA.Text (sha256)
import Daml.Finance.Common.Date.Calendar
import Daml.Finance.Common.Date.DayCount
import Daml.Finance.Common.Date.RollConvention
import Daml.Finance.Common.Date.Schedule
import Daml.Finance.Interface.Asset.Instrument qualified as Instrument (I, createReference, getKey)
import Daml.Finance.Interface.Asset.Types (Id)
import Daml.Finance.Interface.Common.Classes (toUTCTime)
import Daml.Finance.Interface.Common.Disclosure qualified as Disclosure (I, view)
import Daml.Finance.Interface.Derivative.HasClaims qualified as HasClaims (I, getAcquisitionTime, getClaims)
import Daml.Finance.Interface.Derivative.Types (C, Deliverable, Observable, TaggedClaim, taggedClaim)
import Daml.Finance.Interface.Derivative.Util.Claims (isZero', toTime')
import Daml.Finance.Interface.Derivative.Util.Claims.Lifecycle (lifecycle, lifecycleClaims, splitPending, timeEvent)
import Daml.Finance.Interface.Lifecycle.Clock qualified as Clock (I)
import Daml.Finance.Interface.Lifecycle.Effect qualified as Effect (I)
import Daml.Finance.Interface.Lifecycle.Event qualified as Event (I, getEventTime)
import Daml.Finance.Interface.Lifecycle.Lifecyclable qualified as Lifecyclable (I)
import Daml.Finance.Interface.Lifecycle.Observable qualified as Observable (I)
import Daml.Finance.Lifecycle.Effect (Effect(..))
import Daml.Finance.RefData.HolidayCalendar
import Daml.Finance.RefData.Time.DateClock (Unit(..))
import Prelude hiding (key)

-- | Retrieve holiday calendar(s) from the ledger and create a coupon schedule
createCouponSchedule : Date -> [Text] -> BusinessDayConventionEnum -> PeriodEnum -> Int -> Date -> Date -> Party -> Party -> Update Schedule
createCouponSchedule firstCouponDate holidayCalendarIds businessDayConvention couponPeriod couponPeriodMultiplier issueDate maturityDate issuer calendarDataAgency = do
  let
    (y, m, d) = toGregorian firstCouponDate
    periodicSchedule = PeriodicSchedule with
      businessDayAdjustment =
        BusinessDayAdjustment with
          calendarIds = holidayCalendarIds
          convention = businessDayConvention
      effectiveDateBusinessDayAdjustment = None
      terminationDateBusinessDayAdjustment = None
      frequency =
        Frequency with
          rollConvention = DOM d
          period = couponPeriod
          periodMultiplier = couponPeriodMultiplier
      effectiveDate = issueDate
      firstRegularPeriodStartDate = Some firstCouponDate
      lastRegularPeriodEndDate = Some maturityDate
      stubPeriodType = None
      terminationDate = maturityDate
    -- get a holiday calendar from the ledger
    getCalendar holidayCalendarId = do
      exerciseByKey @HolidayCalendar holCalKey GetCalendar with viewer = issuer where
        holCalKey = HolidayCalendarKey with
          agency = calendarDataAgency
          entity = holidayCalendarId
  -- Get the holiday calendars
  cals <- mapA getCalendar holidayCalendarIds
  pure $ createSchedule cals periodicSchedule

-- | Convert the claims to UTCTime and tag them
prepareAndTagClaims : Applicative f => [Claim Date Decimal Deliverable Observable] -> Text -> f [TaggedClaim]
prepareAndTagClaims claim tag = do
  let
    claims = mapClaimToUTCTime $ mconcat claim
  pure [taggedClaim tag claims]

-- | Calculate a fix coupon amount for each coupon date and create claims
createFixRateCouponClaims : (HasField "adjustedEndDate" r Date, HasField "adjustedStartDate" r Date, Applicative f) => [r] -> Decimal -> DayCountConventionEnum -> Deliverable -> f [TaggedClaim]
createFixRateCouponClaims schedule couponRate dayCountConvention cashInstrumentCid = do
  let
    couponDatesAdjusted = map (.adjustedEndDate) schedule
    couponAmounts = map (\p -> couponRate * (calcDcf dayCountConvention p.adjustedStartDate p.adjustedEndDate)) schedule
    couponClaims = zipWith (\d a -> when (TimeGte $ d) $ scale (Const a) $ one cashInstrumentCid) couponDatesAdjusted couponAmounts
  prepareAndTagClaims couponClaims "Fix Coupon"

-- | Calculate a floating coupon amount for each coupon date and create claims
createFloatingRateCouponClaims : (HasField "adjustedEndDate" r Date, HasField "adjustedStartDate" r Date, Applicative f) => [r] -> Decimal -> DayCountConventionEnum -> Deliverable -> Observable -> f [TaggedClaim]
createFloatingRateCouponClaims schedule couponSpread dayCountConvention cashInstrumentCid referenceRateId = do
  let
    couponClaims = map (\p ->
      when (TimeGte $ p.adjustedStartDate) $ scale ((Observe referenceRateId + Const couponSpread) * (Const (calcDcf dayCountConvention p.adjustedStartDate p.adjustedEndDate))) $
      when (TimeGte $ p.adjustedEndDate) $ one cashInstrumentCid) schedule
  prepareAndTagClaims couponClaims "Floating Coupon"

-- | Create a redemption claim
createRedemptionClaim : Applicative f => Deliverable -> Date -> f [TaggedClaim]
createRedemptionClaim cashInstrumentCid maturityDate = do
  let
    redemptionClaim = [when (TimeGte $ maturityDate) $ one cashInstrumentCid]
  prepareAndTagClaims redemptionClaim "Redemption"

-- | HIDE
-- Type-class constraint verified by the bond templates.
type IsBond t =
  ( HasToInterface t Disclosure.I
  , HasToInterface t Instrument.I
  , HasToInterface t HasClaims.I
  , HasToInterface t Lifecyclable.I
  , HasField "id" t Id
  , HasField "issuer" t Party
  , HasField "lastEventTimestamp" t Time
  , HasCreate t
  )

-- | Maps a `Date` to `Time` using the rule in the `DateClock`.
-- From the Daml.Finance.Derivatives.Test file, but could not import here (duplicated for now)
-- In the termsheet only date is mentioned, but lifecycle logic is based on time.
dateToDateClockTime : Date -> Time
dateToDateClockTime = toUTCTime . Unit

-- | Maps a `Date` claim to a `Time` claim using the rule in the `DateClock`.
-- From the Daml.Finance.Derivatives.Test file, but could not import here (duplicated for now)
-- In the termsheet only date is mentioned, but lifecycle logic is based on time.
mapClaimToUTCTime : Claim Date Decimal Deliverable Observable -> C
mapClaimToUTCTime =
  let dateToTime = toUTCTime . Unit in toTime' dateToTime

-- | Rule to process a clock update event.
processClockUpdate : IsBond t => Party -> ContractId Event.I -> ContractId Clock.I -> ContractId Lifecyclable.I -> t -> [ContractId Observable.I] -> Update (ContractId Lifecyclable.I, [ContractId Effect.I])
processClockUpdate settler eventCid _ self instrument observableCids = do
  t <- Event.getEventTime <$> fetch eventCid
  let
    claimInstrument = toInterface @HasClaims.I instrument
    acquisitionTime = HasClaims.getAcquisitionTime claimInstrument

  -- Recover claims tree as of the lastEventTimestamp. For a bond, this just requires lifecycling as of the lastEventTimestamp
  initialClaims <- HasClaims.getClaims claimInstrument
  claims <- Prelude.fst <$> lifecycle observableCids claimInstrument [timeEvent instrument.lastEventTimestamp]

  -- Lifecycle
  (remaining, pending) <- lifecycleClaims observableCids acquisitionTime claims [timeEvent t]
  let
    (consumed, produced) = splitPending pending
  if remaining == claims && null pending then
    pure (self, [])
  else do
    let
      currentKey = Instrument.getKey $ toInterface @Instrument.I instrument
      settlementDate = toDateUTC t -- TODO remove this dependency
      newKey = currentKey with id.version = sha256 $ show remaining
    newInstrumentCid <- create instrument with lastEventTimestamp = t; id = newKey.id
    Instrument.createReference instrument.issuer $ toInterfaceContractId newInstrumentCid
    effectCid <- toInterfaceContractId <$> create Effect with
      provider = currentKey.issuer
      settler
      targetInstrument = currentKey
      producedInstrument = if isZero' remaining then None else Some newKey
      consumed
      produced
      settlementDate
      id = instrument.id.label <> "-" <> show settlementDate
      observers = (.observers) . Disclosure.view $ toInterface @Disclosure.I instrument
    pure (toInterfaceContractId newInstrumentCid, [effectCid])
