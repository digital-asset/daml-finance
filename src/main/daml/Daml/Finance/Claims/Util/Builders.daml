-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

-- | This module includes utility functions used to build contingent claim trees that represent
-- specific payoffs. A `Schedule` is usually used as an input to these utility functions. Given
-- that schedules are defined in terms of dates, a claim where the time parameter is `Date`
-- is returned. These are then mapped to claims where the time parameter is `Time` using a
-- (user-provided) conversion function.
module Daml.Finance.Claims.Util.Builders where

import ContingentClaims.Core.Builders (european)
import ContingentClaims.Core.Claim (Claim, Inequality(..), and, andList, at, cond, give, one, orList, scale, until, upTo, when, zero)
import ContingentClaims.Core.Observation (Observation(..))
import DA.Foldable (foldMap)
import Daml.Finance.Claims.Util (toTime)
import Daml.Finance.Interface.Claims.Types (Deliverable, Observable, TaggedClaim(..))
import Daml.Finance.Interface.Types.Common.Types (InstrumentQuantity)
import Daml.Finance.Interface.Types.Date.DayCount (DayCountConventionEnum)
import Daml.Finance.Interface.Types.Date.Schedule (PeriodicSchedule(..), Schedule)
import Daml.Finance.Util.Date.DayCount (calcPeriodDcf)
import Prelude hiding (and)

type O = Observation Date Decimal Observable
type C = Claim Date Decimal Deliverable Observable

-- | Convert the claims to UTCTime and tag them.
prepareAndTagClaims : (Date -> Time) -> [C] ->
  Text -> TaggedClaim
prepareAndTagClaims dateToTime cs tag =
  let claim = toTime dateToTime $ andList cs
  in TaggedClaim with tag; claim

-- FIXED_RATE_BOND_COUPON_CLAIMS_BEGIN
createFixRatePaymentClaimsList : Schedule -> PeriodicSchedule -> Bool -> Decimal -> Bool ->
  DayCountConventionEnum -> Decimal -> Deliverable -> [C]
createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf couponRate
  ownerReceives dayCountConvention notional cashInstrument =
    let
      couponDatesAdjusted = map (.adjustedEndDate) schedule
      couponAmounts = map (\p ->
          couponRate *
          (calcPeriodDcf dayCountConvention p useAdjustedDatesForDcf
            periodicSchedule.terminationDate periodicSchedule.frequency)
        ) schedule
      couponClaimAmounts = andList $
        zipWith
          (\d a ->
            when (TimeGte d) $ scale (Const a) $ scale (Const notional) $ one cashInstrument
          ) couponDatesAdjusted couponAmounts
    in
      [if ownerReceives then couponClaimAmounts else give couponClaimAmounts]
-- FIXED_RATE_BOND_COUPON_CLAIMS_END

-- | Calculate a fix rate amount for each payment date and create claims.
createFixRatePaymentClaims : (Date -> Time) -> Schedule -> PeriodicSchedule -> Bool -> Decimal ->
  Bool -> DayCountConventionEnum -> Decimal -> Deliverable -> TaggedClaim
createFixRatePaymentClaims dateToTime schedule periodicSchedule useAdjustedDatesForDcf
  couponRate ownerReceives dayCountConvention notional cashInstrument =
    let
      couponClaims = createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf
        couponRate ownerReceives dayCountConvention notional cashInstrument
    in prepareAndTagClaims dateToTime couponClaims "Fix rate payment"

-- | Calculate a fix rate amount (if a credit event has not yet happened) for each payment date and
-- create claims.
createConditionalCreditFixRatePaymentClaims : (Date -> Time) -> Schedule -> PeriodicSchedule ->
  Bool -> Decimal -> Bool -> DayCountConventionEnum -> Decimal -> Deliverable -> Observable ->
  TaggedClaim
createConditionalCreditFixRatePaymentClaims dateToTime schedule periodicSchedule
  useAdjustedDatesForDcf couponRate ownerReceives dayCountConvention notional
  cashInstrument defaultProbabilityReferenceId
  = let
      couponClaims = createFixRatePaymentClaimsList schedule periodicSchedule useAdjustedDatesForDcf
        couponRate ownerReceives dayCountConvention notional cashInstrument
      creditEvent = Lte (Const 1.0, Observe defaultProbabilityReferenceId)
      couponClaimUntilCreditEvent =
        when (TimeGte periodicSchedule.effectiveDate)
        $ until creditEvent
        $ andList couponClaims
    in
      prepareAndTagClaims dateToTime [couponClaimUntilCreditEvent]
        "Fix rate payment (unless credit event has occurred)"

-- | Calculate a (1-recoveryRate) payment if a credit event just happened and create claims.
createCreditEventPaymentClaims : (Date -> Time) -> Bool -> Decimal -> Deliverable -> Observable ->
  Observable -> PeriodicSchedule -> TaggedClaim
createCreditEventPaymentClaims dateToTime ownerReceives notional cashInstrument
  defaultProbabilityReferenceId recoveryRateReferenceId periodicSchedule =
  let
    creditEvent = Lte (Const 1.0, Observe defaultProbabilityReferenceId)
    payoffAmount =
      scale ((Const 1.0 - Observe recoveryRateReferenceId) * Const notional)
      $ one cashInstrument
    payoff = if ownerReceives then payoffAmount else give payoffAmount
    creditEventClaim =
      when (TimeGte periodicSchedule.effectiveDate)
      $ when creditEvent
      $ when (upTo periodicSchedule.terminationDate) payoff
  in prepareAndTagClaims dateToTime [creditEventClaim] "Credit event payment"

-- FLOATING_RATE_BOND_COUPON_CLAIMS_BEGIN
-- | Calculate a floating rate amount for each payment date and create claims.
-- The floating rate is always observed on the first day of each payment period and used for the
-- corresponding payment on the last day of that payment period. This means that the calculation
-- agent needs to provide such an Observable, irrespective of the kind of reference rate used (e.g.
-- a forward looking LIBOR or a backward looking SOFR-COMPOUND).
createFloatingRatePaymentClaims : (Date -> Time) -> Schedule -> PeriodicSchedule -> Bool ->
  Decimal -> Bool -> DayCountConventionEnum -> Decimal -> Deliverable -> Observable -> TaggedClaim
createFloatingRatePaymentClaims dateToTime schedule periodicSchedule useAdjustedDatesForDcf
  floatingRateSpread ownerReceives dayCountConvention notional cashInstrument referenceRateId =
  let
    couponClaimAmounts = andList $ map (\p ->
        when (TimeGte p.adjustedEndDate)
        $ scale (
          (ObserveAt referenceRateId p.adjustedStartDate + Const floatingRateSpread) *
          (Const (calcPeriodDcf dayCountConvention p useAdjustedDatesForDcf
                    periodicSchedule.terminationDate periodicSchedule.frequency)
          ))
        $ scale (Const notional)
        $ one cashInstrument
      ) schedule
    couponClaims = if ownerReceives then couponClaimAmounts else give couponClaimAmounts
  in prepareAndTagClaims dateToTime [couponClaims] "Floating rate payment"
-- FLOATING_RATE_BOND_COUPON_CLAIMS_END

-- ASSET_PERFORMANCE_CLAIMS_BEGIN
-- | Calculate the asset performance for each payment date and create claims.
-- The performance is calculated using the reference asset from the start date to the end date of
-- each payment period. The reference asset Observable needs to contain the appropriate type of
-- fixings:
--   - unadjusted fixings in case of a price return asset swap
--   - adjusted fixings in case of a total return asset swap
createAssetPerformancePaymentClaims : (Date -> Time) -> Schedule -> PeriodicSchedule -> Bool ->
  Bool -> DayCountConventionEnum -> Decimal -> Deliverable -> Observable -> TaggedClaim
createAssetPerformancePaymentClaims dateToTime schedule periodicSchedule useAdjustedDatesForDcf
  ownerReceives dayCountConvention notional cashInstrument referenceAssetId =
  let
    assetClaimAmounts = foldMap (\p ->
      let
        performance =
          Observe referenceAssetId / ObserveAt referenceAssetId p.adjustedStartDate - Const 1.0
      in
        when (TimeGte p.adjustedEndDate)
          $ scale (Const notional * performance)
          $ one cashInstrument
      ) schedule
    assetClaims = if ownerReceives then assetClaimAmounts else give assetClaimAmounts
  in
    prepareAndTagClaims dateToTime [assetClaims] "Asset performance payment"
-- ASSET_PERFORMANCE_CLAIMS_END

-- FIXED_RATE_BOND_REDEMPTION_CLAIM_BEGIN
-- | Create an FX adjusted principal claim.
-- This can be used for both FX swaps (using the appropriate FX rate) and single currency bonds
-- (setting the FX rate to 1.0).
createFxAdjustedPrincipalClaim : (Date -> Time) -> Bool -> Decimal -> Decimal -> Deliverable ->
  Date -> TaggedClaim
createFxAdjustedPrincipalClaim dateToTime ownerReceives fxRateMultiplier notional
  cashInstrument valueDate =
  let
    fxLegClaimAmount = when (TimeGte valueDate)
                       $ scale (Const fxRateMultiplier)
                       $ scale (Const notional)
                       $ one cashInstrument
    fxLegClaim = if ownerReceives then fxLegClaimAmount else give fxLegClaimAmount
  in
    prepareAndTagClaims dateToTime [fxLegClaim] "Principal payment"
-- FIXED_RATE_BOND_REDEMPTION_CLAIM_END

-- | Create the claim for a long vanilla option (cash-settled, automatically exercised).
createVanillaOptionClaim : (Date -> Time) -> Decimal -> Observable -> Deliverable -> Date -> Bool
  -> Bool -> Decimal -> C
createVanillaOptionClaim dateToTime strike spot cashInstrument expiryDate isCall
  percentageOfStrike notionalMultiplier =
  let
    intrinsicValueAbs = if isCall then Observe spot - Const strike
                                  else Const strike - Observe spot
    intrinsicValue = if percentageOfStrike
      then intrinsicValueAbs / Const strike
      else intrinsicValueAbs
    exercised = scale (Const notionalMultiplier * intrinsicValue) $ one cashInstrument
    notExercised = zero
    autoExerciseCondition = if isCall then Lte (Const strike, Observe spot)
                                      else Lte (Observe spot, Const strike)
  in
    when (TimeGte expiryDate) $ cond autoExerciseCondition exercised notExercised

-- | Create the claim for a cash-settled, automatically exercised option (long or short).
createEuropeanCashClaim : (Date -> Time) -> Bool -> Decimal -> Observable -> Deliverable -> Date ->
  Bool -> Bool -> Decimal -> TaggedClaim
createEuropeanCashClaim dateToTime isLong strike spot cashInstrument expiryDate
  isCall percentageOfStrike notionalMultiplier =
  let
    claimAmount = createVanillaOptionClaim dateToTime strike spot cashInstrument expiryDate isCall
      percentageOfStrike notionalMultiplier
    claim = if isLong then claimAmount else give claimAmount
  in
    prepareAndTagClaims dateToTime [claim] "European option payment"

-- | Create the claim for a barrier option (automatically exercised, cash-settled).
createBarrierEuropeanCashClaim : (Date -> Time) -> Bool -> Decimal -> Observable -> Deliverable
  -> Date -> Bool -> Decimal -> Date -> Bool -> Bool -> Bool -> Decimal -> TaggedClaim
createBarrierEuropeanCashClaim dateToTime isLong strike spot cashInstrument
  expiryDate isCall barrier barrierStartDate isDown isOut percentageOfStrike notionalMultiplier =
  let
    barrierEvent = if isDown then Lte (Observe spot, Const barrier)
                             else Lte (Const barrier, Observe spot)
    vanillaOption = createVanillaOptionClaim dateToTime strike spot cashInstrument expiryDate isCall
      percentageOfStrike notionalMultiplier
    barrierClaimAmountExclStartDate = if isOut
      then vanillaOption `and` when barrierEvent (when (TimeLte expiryDate) (give vanillaOption))
      else when barrierEvent $ when (TimeLte expiryDate) vanillaOption
    claimAmount = when (at barrierStartDate) barrierClaimAmountExclStartDate
    claim = if isLong then claimAmount else give claimAmount
  in
    prepareAndTagClaims dateToTime [claim] "Barrier option payment"

-- | Create the claim for a physically settled European option.
createEuropeanPhysicalClaim : (Date -> Time) -> Bool -> Decimal -> Deliverable -> Deliverable ->
  Date -> Bool -> TaggedClaim
createEuropeanPhysicalClaim dateToTime isLong strike underlying ccy
  expiryDate isCall =
  let
    exerciseClaim =
      if isCall then
        (one underlying) `and` scale (Const strike) (give (one ccy))
      else
        give (one underlying) `and` scale (Const strike) (one ccy)
    claimAmount = european expiryDate exerciseClaim
    claim = if isLong then claimAmount else give claimAmount
  in
    prepareAndTagClaims dateToTime [claim] "Option payment"

-- | Create the claim for a physically settled Dividend option.
createDividendOptionClaim : (Date -> Time) -> Date -> InstrumentQuantity ->
  Optional InstrumentQuantity -> Optional InstrumentQuantity -> TaggedClaim
createDividendOptionClaim dateToTime expiryDate cashQuantity
  sharesQuantity fxQuantity =
  let
    cashClaim = scale (Const cashQuantity.amount) (one cashQuantity.unit)
    taggedCashClaims = [("CASH", cashClaim)]

    taggedSharesClaims = case sharesQuantity of
      None -> []
      Some shares -> [("SHARES", sharesClaim)]
        where
          sharesClaim = scale (Const shares.amount) (one shares.unit)

    taggedFxClaims = case fxQuantity of
      None -> []
      Some fx -> [("CASHFX", cashFxClaim)]
        where
          cashFxClaim = scale (Const fx.amount) $ one fx.unit

    cashOrSharesOrFx = orList $ taggedCashClaims ++ taggedSharesClaims ++ taggedFxClaims
    claim = when (at expiryDate) cashOrSharesOrFx
  in
    prepareAndTagClaims dateToTime [claim] "Option payment"
