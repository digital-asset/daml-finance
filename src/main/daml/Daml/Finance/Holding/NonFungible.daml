-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Holding.NonFungible where

import DA.Set (fromList, singleton)
import Daml.Finance.Holding.Util (transferImpl)
import Daml.Finance.Interface.Holding.Base qualified as Base (I, View(..))
import Daml.Finance.Interface.Holding.Factory qualified as HoldingFactory (Create(..), F, View(..))
import Daml.Finance.Interface.Holding.Transferable qualified as Transferable (I, View(..))
import Daml.Finance.Interface.Types.Common.Types (AccountKey(..), InstrumentKey(..), Id, PartiesMap)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, View(..), flattenObservers)
import Daml.Finance.Interface.Util.Lockable qualified as Lockable (I, Lock(..), View(..), getLockers)
import Daml.Finance.Util.Disclosure (addObserversImpl, removeObserversImpl, setObserversImpl)
import Daml.Finance.Util.Lockable (acquireImpl, isValidLock, releaseImpl)

-- | Type synonym for `Factory`.
type F = Factory

-- | Type synonym for `NonFungible`.
type T = NonFungible

-- | Implementation of a non-fungible holding.
-- `NonFungible` implements the interface `Transferable.I` (which requires `Base.I` and
-- `Disclosure.I` to be implemented).
template NonFungible
  with
    instrument : InstrumentKey
      -- ^ The instrument of which units are held.
    account : AccountKey
      -- ^ The account at which the holding is held. Defines the holding's owner and custodian.
    amount : Decimal
      -- ^ Number of units.
    lock : Optional Lockable.Lock
      -- ^ An optional lock for the holding.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory account.custodian, account.owner, Lockable.getLockers this
    observer Disclosure.flattenObservers observers

    ensure amount > 0.0 && isValidLock lock

    interface instance Disclosure.I for NonFungible where
      view = Disclosure.View with
        disclosureControllers = fromList [account.owner, account.custodian]; observers
      setObservers = setObserversImpl @_ @Disclosure.I this None
      addObservers = addObserversImpl @_ @Disclosure.I this None
      removeObservers = removeObserversImpl @_ @Disclosure.I this None

    interface instance Lockable.I for NonFungible where
      view = Lockable.View with lock; controllers = singleton account.owner
      acquire = acquireImpl this.lock (\lock -> this with lock)
      release = releaseImpl this.lock (\lock -> this with lock)

    interface instance Base.I for NonFungible where
      view = Base.View with instrument; account; amount

    interface instance Transferable.I for NonFungible where
      view = Transferable.View {}
      transfer = transferImpl $ toInterface this

-- | Implementation of a factory template for non-fungible holdings.
template Factory
  with
    provider : Party
      -- ^ The factory's provider.
    id : Id
      -- ^ Identifier for the holding factory.
    observers : PartiesMap
      -- ^ The factory's observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance HoldingFactory.F for Factory
      where
        view = HoldingFactory.View with provider; id
        create' HoldingFactory.Create{instrument; account; amount; observers} = do
          assertMsg "amount must be positive" $ amount > 0.0
          toInterfaceContractId <$>
            create NonFungible with instrument; account; amount; observers; lock = None

    interface instance Disclosure.I for Factory where
      view = Disclosure.View with disclosureControllers = singleton provider; observers
      setObservers = setObserversImpl @Factory @Disclosure.I this None
      addObservers = addObserversImpl @Factory @Disclosure.I this None
      removeObservers = removeObserversImpl @Factory @Disclosure.I this None
