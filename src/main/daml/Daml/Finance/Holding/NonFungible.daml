-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE AllowAmbiguousTypes #-}

module Daml.Finance.Holding.NonFungible where

import DA.Assert ((===))
import DA.Set (singleton)
import Daml.Finance.Holding.NonTransferable (acquireImpl, releaseImpl)
import Daml.Finance.Holding.Util (reapplyLocks)
import Daml.Finance.Interface.Holding.Account qualified as Account (Credit(..), I, R, exerciseInterfaceByKey)
import Daml.Finance.Interface.Holding.Base qualified as Base (I, View(..))
import Daml.Finance.Interface.Holding.Factory.Holding qualified as HoldingFactory (Create(..), F, HasImplementation(..), Remove(..), View(..))
import Daml.Finance.Interface.Holding.Lockable qualified as Lockable (I, Lock(..), View(..), asHolding)
import Daml.Finance.Interface.Holding.Transferable qualified as NonFungible (HasImplementation, I, Transfer(..), View(..), asLockable)
import Daml.Finance.Interface.Holding.Util (getLocker)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (qty)
import Daml.Finance.Interface.Types.Common (AccountKey(..), InstrumentKey(..), PartiesMap)
import Daml.Finance.Interface.Util.Common (fetchInterfaceByKey)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, SetObservers(..), View(..), flattenObservers)
import Prelude hiding (null)

-- | Type synonym for `Factory`.
type F = Factory

-- | Type synonym for `NonFungible`.
type T = NonFungible

instance NonFungible.HasImplementation T
-- | Implementation of a non-fungible holding.
-- `NonFungible` implements the interface `Transferable.I` (which requires `Lockable.I`,
-- `Base.I`, and `Disclosure.I` to be implemented as well).
template NonFungible
  with
    instrument : InstrumentKey
      -- ^ The instrument of which units are held.
    account : AccountKey
      -- ^ The account at which the holding is held. Defines the holding's owner and custodian.
    amount : Decimal
      -- ^ Number of units.
    lock : Optional Lockable.Lock
      -- ^ An optional lock of a holding.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory account.custodian, account.owner, getLocker this
    observer Disclosure.flattenObservers observers

    ensure amount > 0.0

    interface instance Disclosure.I for NonFungible where
      view = Disclosure.View with disclosureControllers = singleton account.owner; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive $ fromInterfaceContractId @NonFungible self

    interface instance Base.I for NonFungible where
      asDisclosure = toInterface @Disclosure.I this
      view = Base.View with instrument; account; amount

    interface instance Lockable.I for NonFungible where
      asHolding = toInterface @Base.I this
      view = Lockable.View with lock
      acquire = acquireImpl @NonFungible (toInterface @Lockable.I this)
      release = releaseImpl @NonFungible (toInterface @Lockable.I this)

    interface instance NonFungible.I for NonFungible where
      asLockable = toInterface @Lockable.I this
      view = NonFungible.View {}
      transfer = transferImpl @NonFungible (toInterface @NonFungible.I this)

-- | Implementatino of the transfer choice.
transferImpl : forall t. (HasTemplateTypeRep t) => NonFungible.I -> NonFungible.Transfer -> Update (ContractId NonFungible.I)
transferImpl transferable NonFungible.Transfer{newOwnerAccount} = do
  --
  let
    l = NonFungible.asLockable transferable
    vL = view l
    vH = view $ Lockable.asHolding l
  -- Account sanity checks
  newAccount <- fetchInterfaceByKey @Account.R newOwnerAccount
  let vA = view newAccount
  vA.owner === newOwnerAccount.owner
  vA.custodian === vH.account.custodian
  -- Create new holding via Credit
  newHoldingCid <- coerceContractId <$> Account.exerciseInterfaceByKey @Account.I newOwnerAccount newOwnerAccount.owner Account.Credit
    with
      quantity = Instrument.qty vH.amount vH.instrument
  -- Holding sanity checks. This prevents a holding from changing type during a `Transfer` (from e.g., `NonFungible` to `Fungible`)
  newHolding <- fetch newHoldingCid
  assertMsg "Sent holding type does not correspond to receiving account holding type" $ interfaceTypeRep newHolding == templateTypeRep @t
  -- Reapply holding locks
  coerceContractId <$> case vL.lock of
    Some lock -> reapplyLocks lock newHoldingCid
    None -> pure newHoldingCid

instance HoldingFactory.HasImplementation Factory
-- | Implementation of the corresponding Holding Factory.
template Factory
  with
    provider : Party
      -- ^ The factory's provider.
    observers : PartiesMap
      -- ^ The factory's observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance HoldingFactory.F for Factory
      where
        asDisclosure = toInterface @Disclosure.I this
        view = HoldingFactory.View with provider
        create' HoldingFactory.Create{instrument; account; amount; observers} =
          toInterfaceContractId <$> create NonFungible with instrument; account; amount; observers; lock = None
        remove HoldingFactory.Remove{actors; holdingCid} = archive $ fromInterfaceContractId @NonFungible holdingCid

    interface instance Disclosure.I for Factory where
      view = Disclosure.View with disclosureControllers = singleton provider; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId NonFungible)
