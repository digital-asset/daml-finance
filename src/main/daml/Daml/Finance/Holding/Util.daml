-- Copyright (c) 2023 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

-- | Module providing default implementations for the `Transfer`, `Split`, and `Merge` choices.
-- These functions are used across this package but can also be used in custom holding
-- implementations.
module Daml.Finance.Holding.Util where

import DA.Action (foldlA)
import DA.Foldable qualified as F (all)
import DA.Optional (isNone)
import DA.Set qualified as S (fromList, isSubsetOf)
import DA.Traversable qualified as T (forA)
import Daml.Finance.Interface.Account.Account qualified as Account (Credit(..), Debit(..), I, R, exerciseInterfaceByKey)
import Daml.Finance.Interface.Account.Util (fetchAccount)
import Daml.Finance.Interface.Holding.Base qualified as Base (I)
import Daml.Finance.Interface.Holding.Fungible qualified as Fungible (I, Merge(..), Split(..), SplitResult(..))
import Daml.Finance.Interface.Holding.Transferable qualified as Transferable (I, Transfer(..))
import Daml.Finance.Interface.Util.Common (fetchInterfaceByKey, qty, verify)
import Daml.Finance.Interface.Util.Lockable qualified as Lockable (I)

-- | Default implementation of `transfer` for the `Transferable` interface.
transferImpl : Transferable.I -> ContractId Transferable.I -> Transferable.Transfer
  -> Update (ContractId Transferable.I)
transferImpl this self Transferable.Transfer{actors; newOwnerAccount} = do
  let
    baseCid = toInterfaceContractId @Base.I self
    base = toInterface @Base.I this
  account <- fetchAccount base
  let
    vBase = view base
    vLockable = view $ toInterface @Lockable.I this
    vAccount = view account
  assertMsg "Holding must not be locked" $ isNone vLockable.lock
  vNewAccount <- view <$> fetchInterfaceByKey @Account.R @Account.I newOwnerAccount
  -- Verify
  assertMsg "Actors must contain all transfer outgoing of the sender account"
    $ vAccount.controllers.outgoing `S.isSubsetOf` actors
  assertMsg "Actors must contain all transfer incoming of the receiving account"
    $ vNewAccount.controllers.incoming `S.isSubsetOf` actors
  assertMsg "Custodians must be the same" $ vBase.account.custodian == vAccount.custodian
  -- Debit
  Account.exerciseInterfaceByKey @Account.I vBase.account vBase.account.custodian
    Account.Debit with holdingCid = baseCid
  -- Credit
  newBaseCid <- Account.exerciseInterfaceByKey @Account.I newOwnerAccount newOwnerAccount.custodian
    Account.Credit with quantity = qty vBase.amount vBase.instrument
  -- Consistency check
  newBase <- fetch newBaseCid
  assertMsg "credited and debited holding types must match" $
    interfaceTypeRep newBase == interfaceTypeRep base
  pure $ fromInterfaceContractId newBaseCid

-- | Default implementation of `split` from the `Fungible` interface.
splitImpl :
  ( HasCreate t
  , HasToInterface t Fungible.I
  ) => Fungible.I -> (Decimal -> t) -> Fungible.Split -> Update Fungible.SplitResult
splitImpl this setAmount Fungible.Split{amounts} = do
  let
    base = toInterface @Base.I this
    vBase = view base
    vLockable = view $ toInterface @Lockable.I this
  assertMsg "Holding must not be locked" $ isNone vLockable.lock
  let spawn = fmap toInterfaceContractId . create . setAmount
  splitCids <- forA amounts spawn
  restAmount <- getRestAmount amounts vBase.amount
  rest <- T.forA restAmount spawn
  pure Fungible.SplitResult with splitCids; rest

-- | Default implementation of `merge` from the `Fungible` interface.
mergeImpl :
  forall t.
  ( HasCreate t
  , HasArchive t
  , HasSignatory t
  , HasFromInterface t Fungible.I
  , HasToInterface t Fungible.I
  )
  => Fungible.I
  -> (t -> Decimal)
  -> (Decimal -> t)
  -> Fungible.Merge
  -> Update (ContractId Fungible.I)
mergeImpl this getAmount setAmount Fungible.Merge{fungibleCids} = do
  let
    base = toInterface @Base.I this
    vBase = view base
    vLockable = view $ toInterface @Lockable.I this
  assertMsg "Holding must not be locked" $ isNone vLockable.lock
  assertMsg "List of fungibles must be non-empty" . not . null $ fungibleCids
  let
    currentSignatories = S.fromList . signatory . setAmount $ vBase.amount
    current = view . toInterface @Base.I . toInterface @Fungible.I . setAmount $ vBase.amount
    aggregate aggregatedAmount fungibleCid = do
      Some (otherHoldingCid, otherHolding) <- fetchFromInterface @t fungibleCid
      let
        otherBase = toInterface @Base.I . toInterface @Fungible.I $ otherHolding
        other = view otherBase
        otherLock = view $ toInterface @Lockable.I otherBase
      assertMsg "Instrument must match" $ other.instrument == current.instrument
      assertMsg "Account must match" $ other.account == current.account
      assertMsg "Signatories must match" $ S.fromList (signatory otherHolding) == currentSignatories
      assertMsg "Other holding must not be locked" $ isNone otherLock.lock
      archive otherHoldingCid
      pure $ aggregatedAmount + getAmount otherHolding
  tot <- foldlA aggregate vBase.amount fungibleCids
  fmap toInterfaceContractId . create . setAmount $ tot

-- | HIDE
-- Gets the rest amount of a split (if any) and verifies that split amounts are strictly positive
-- and don't exceed the current amount.
getRestAmount : CanAssert m => [Decimal] -> Decimal -> m (Optional Decimal)
getRestAmount amounts currentAmount = do
  let splitAmountSum = sum amounts
  verify (splitAmountSum <= currentAmount && F.all (> 0.0) amounts && amounts /= []) $ "Fungible::"
    <> "verifySplit - Amounts must be non-empty, strictly positive, and not exceed current amount. "
    <> "amounts=" <> show amounts
    <> ", splitAmountSum=" <> show splitAmountSum
    <> ", currentAmount=" <> show currentAmount
  let rest = currentAmount - splitAmountSum
  pure $ if rest == 0.0 then None else Some rest
