-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

{-# LANGUAGE AllowAmbiguousTypes #-}

module Daml.Finance.Holding.NonTransferable where

import DA.Set (singleton)
import Daml.Finance.Holding.Util (acquireImpl, releaseImpl)
import Daml.Finance.Interface.Holding.Base qualified as Base (I, View(..))
import Daml.Finance.Interface.Holding.Factory.Holding qualified as HoldingFactory (Create(..), F, HasImplementation(..), Remove(..), View(..))
import Daml.Finance.Interface.Holding.Lockable (getLocker)
import Daml.Finance.Interface.Holding.Lockable qualified as Lockable (I, Lock(..), View(..))
import Daml.Finance.Interface.Holding.Lockable qualified as NonTransferable (HasImplementation)
import Daml.Finance.Interface.Types.Common (AccountKey(..), InstrumentKey(..), PartiesMap)
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I, SetObservers(..), View(..), flattenObservers)
import Prelude hiding (null)

-- | Type synonym for `Factory`.
type F = Factory

-- | Type synonym for `NonTransferable`.
type T = NonTransferable

instance NonTransferable.HasImplementation T
-- | Implementation of a non-transferable holding.
-- `NonTransferable` implements the interface `Lockable.I` (which requires `Base.I`, and
-- `Disclosure.I` to be implemented as well).
template NonTransferable
  with
    instrument : InstrumentKey
      -- ^ The instrument of which units are held.
    account : AccountKey
      -- ^ The account at which the holding is held. Defines the holding's owner and custodian.
    amount : Decimal
      -- ^ Number of units.
    lock : Optional Lockable.Lock
      -- ^ An optional lock of a holding.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory account.custodian, account.owner, getLocker this
    observer Disclosure.flattenObservers observers

    ensure amount > 0.0

    interface instance Disclosure.I for NonTransferable where
      view = Disclosure.View with disclosureControllers = singleton account.owner; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive $ fromInterfaceContractId @NonTransferable self

    interface instance Base.I for NonTransferable where
      asDisclosure = toInterface @Disclosure.I this
      view = Base.View with instrument; account; amount

    interface instance Lockable.I for NonTransferable where
      asHolding = toInterface @Base.I this
      view = Lockable.View with lock; account; instrument
      acquire = acquireImpl @NonTransferable this (toInterface @Lockable.I this)
      release = releaseImpl @NonTransferable this (toInterface @Lockable.I this)

instance HoldingFactory.HasImplementation Factory
-- | Implementation of the corresponding Holding Factory.
template Factory
  with
    provider : Party
      -- ^ The factory's provider.
    observers : PartiesMap
      -- ^ The factory's observers.
  where
    signatory provider
    observer Disclosure.flattenObservers observers

    interface instance HoldingFactory.F for Factory
      where
        asDisclosure = toInterface @Disclosure.I this
        view = HoldingFactory.View with provider
        create' HoldingFactory.Create{instrument; account; amount; observers} =
          toInterfaceContractId <$> create NonTransferable with instrument; account; amount; observers; lock = None
        remove HoldingFactory.Remove{actors; holdingCid} = archive $ fromInterfaceContractId @NonTransferable holdingCid

    interface instance Disclosure.I for Factory where
      view = Disclosure.View with disclosureControllers = singleton provider; observers
      setObservers Disclosure.SetObservers{newObservers} = toInterfaceContractId <$> create this with observers = newObservers
      archive' self = archive (coerceContractId self : ContractId NonTransferable)
