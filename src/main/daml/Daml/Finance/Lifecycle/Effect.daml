-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Lifecycle.Effect where

import DA.Assert ((===))
import Daml.Finance.Interface.Holding.Util (getAmount, getInstrumentKey)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (K, Q, scale)
import Daml.Finance.Interface.Lifecycle.Effect qualified as Effect (Calculate(..), CalculationResult(..), HasImplementation, I, SetProvider(..), View(..))
import Daml.Finance.Interface.Types.Common (Id, Parties)

-- | Type synonym for `Effect`.
type T = Effect

instance Effect.HasImplementation T

-- | A contract encoding the consequences of a lifecycle event for one unit of the target instrument.
template Effect
  with
    provider : Parties
      -- ^ The effect provider.
    settlers : Parties
      -- ^ Any of the parties can settle the effect's consequences.
    id : Id
      -- ^ The effect's identifier.
    description : Text
      -- ^ The effect's description.
    targetInstrumentKey : Instrument.K
      -- ^ The target instrument.
    producedInstrumentKey : Optional Instrument.K
      -- ^ The produced instrument, when it exists.
    consumed : [Instrument.Q]
      -- ^ Consumed quantities (not including the target instrument).
    produced : [Instrument.Q]
      -- ^ Produced quantities (not including the produced instrument).
    settlementDate : Date
      -- ^ The effect's settlement date.
    observers : Parties
      -- ^ Observers.
  where
    signatory provider
    observer observers

    interface instance Effect.I for Effect where
      view = Effect.View with provider; settlers; id; description; targetInstrumentKey; producedInstrumentKey; consumed; produced; settlementDate

      calculate Effect.Calculate{holdingCid} _ = do
        holding <- fetch holdingCid
        getInstrumentKey holding === targetInstrumentKey
        let scaleAll = fmap . Instrument.scale $ getAmount holding
        pure Effect.CalculationResult with
          consumed = scaleAll consumed
          produced = scaleAll produced

      setProvider Effect.SetProvider{newProvider} = toInterfaceContractId <$> create this with provider = newProvider
