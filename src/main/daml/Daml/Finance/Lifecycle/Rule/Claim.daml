-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Lifecycle.Rule.Claim where

import DA.Foldable (forA_)
import DA.Set (member)
import Daml.Finance.Interface.Holding.Account qualified as Account (exerciseInterfaceByKey, Debit(..), Credit(..), I)
import Daml.Finance.Interface.Holding.Util (getAccountKey, getAmount, getCustodian, getInstrumentKey, getOwner)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (qty)
import Daml.Finance.Interface.Lifecycle.Effect qualified as Effect (CalculationResult(..), Calculate(..), GetView(..))
import Daml.Finance.Interface.Lifecycle.Rule.Claim qualified as Claim (ClaimResult(..), ClaimEffect(..), HasImplementation(..), I, View(..))
import Daml.Finance.Interface.Settlement.Factory qualified as Factory (I, Instruct(..))
import Daml.Finance.Interface.Settlement.Types (Step(..))
import Daml.Finance.Interface.Types.Common (Parties)

-- | Type synonym for `Rule`.
type T = Rule

instance Claim.HasImplementation Rule

-- | Rule contract that allows an actor to claim effects, returning settlement instructions.
template Rule
  with
    providers : Parties
      -- ^ Providers of the claim rule. Together with the actors of the `ClaimEffect` choice the authorization requirements to upgrade the holdings being claimed have to be met.
    claimers : Parties
      -- ^ Any of the parties can claim an effect.
    settlers : Parties
      -- ^ Any of the parties can trigger settlement of the resulting batch.
    settlementFactoryCid : ContractId Factory.I
      -- ^ Settlement factory contract used to create a `Batch` of `Instruction`\s.
  where
    signatory providers

    interface instance Claim.I for Rule where
      view = Claim.View with providers; claimers; settlers; settlementFactoryCid
      claimEffect Claim.ClaimEffect{claimer; holdingCids; effectCid; batchId} = do
        assertMsg "Effect can only be claimed by authorized parties." $ claimer `member` this.claimers
        effectView <- exercise effectCid Effect.GetView with viewer = claimer
        holdings <- mapA fetch holdingCids
        forA_ holdings \h -> do
          assertMsg "The provided holding does not reference the expected instrument." $ getInstrumentKey h == effectView.targetInstrumentKey
          assertMsg "The settlement rule is missing the signature of a holding custodian" $ getCustodian h `member` providers
          assertMsg "The settlement rule is missing the signature of a holding owner" $ getOwner h `member` providers

        -- Calculate settlement steps
        let
          createSteps consume quantities holding =
            let
              sender = if consume then getOwner holding else getCustodian holding
              receiver = if consume then getCustodian holding else getOwner holding
            in map (\quantity -> Step with sender; receiver; quantity) quantities

          calculateSteps holding holdingCid = do
            Effect.CalculationResult with consumed; produced <- exercise effectCid Effect.Calculate with actor = claimer; holdingCid
            let
              consumedSteps = createSteps True consumed holding
              producedSteps = createSteps False produced holding
            pure $ consumedSteps <> producedSteps

        steps <- mconcat <$> sequence (zipWith calculateSteps holdings holdingCids)

        -- Update holding on target instrument
        let
          updateInstrumentHolding (holding, holdingCid) = do
            let
              account = getAccountKey holding
              amount = getAmount holding
            -- Debit old instrument
            Account.exerciseInterfaceByKey @Account.I account claimer Account.Debit with holdingCid
            -- Credit new instrument
            case effectView.producedInstrumentKey of
              Some pi -> Some <$> Account.exerciseInterfaceByKey @Account.I account claimer Account.Credit with quantity = Instrument.qty amount pi
              None -> pure None

        newInstrumentHoldingCids <- fmap sequence . mapA updateInstrumentHolding $ zip holdings holdingCids

        -- Generate settlement instructions for other instruments
        (batchCid, instructionCids) <- exercise settlementFactoryCid Factory.Instruct
          with
            instructors = providers
            settlers
            id = batchId
            description = effectView.description
            contextId = Some effectView.id
            steps
        pure Claim.ClaimResult with newInstrumentHoldingCids; batchCid; instructionCids
