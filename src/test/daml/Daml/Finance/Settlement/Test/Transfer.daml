-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Settlement.Test.Transfer where

import DA.Map qualified as M (fromList)
import DA.Set (empty, singleton)
import Daml.Finance.Holding.Fungible qualified as Fungible (Factory(..))
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (qty)
import Daml.Finance.Interface.Settlement.Batch qualified as Batch (Settle(..))
import Daml.Finance.Interface.Settlement.Factory qualified as Factory (I, Instruct(..))
import Daml.Finance.Interface.Settlement.Instruction qualified as Instruction (Allocate(..), Approve(..))
import Daml.Finance.Interface.Settlement.Types (Allocation(..), Approval(..), Step(..))
import Daml.Finance.Interface.Types.Common (Id(..))
import Daml.Finance.Settlement.Factory (Factory(..))
import Daml.Finance.Test.Util.Account qualified as Account (credit, createAccount, createFactory)
import Daml.Finance.Test.Util.Common (createParties)
import Daml.Finance.Test.Util.Holding qualified as Holding (verifyNoObservers, verifyOwnerOfHolding)
import Daml.Finance.Test.Util.Instrument qualified as Instrument (originate)
import Daml.Script

-- | Parties involved in the test script.
data TestParties = TestParties
  with
    bank : Party
      -- ^ Acts as custodian in the respective holdings.
    cb : Party
      -- ^ Depository and issuer of the cash instrument.
    sender : Party
      -- ^ Sends units of cash to receiver.
    receiver : Party
      -- ^ Receives units of cash from sender.
    provider : Party
      -- ^ Acts as provider of account and holding factory.
    publicParty : Party
      -- ^ The public party. Every party can readAs the public party.

-- | Transfer of cash between two parties (sender -> receiver) facilitated by an intermediary (bank)
-- +------------------------------------------+
-- | Accounts                                 |
-- +------------------+-----------------------+
-- | structure:       | used for:             |
-- +------------------+-----------------------+
-- |      Bank        |                       |
-- |      /  \        | commercial bank money |
-- | Sender  Receiver |                       |
-- +------------------+-----------------------+
run : Script ()
run = script do
  TestParties{..} <- setupParties

  -- Account and holding factory
  let fp = [("FactoryProvider", singleton publicParty)]
  accountFactoryCid <- toInterfaceContractId <$> Account.createFactory provider fp
  holdingFactoryCid <- toInterfaceContractId <$> submitMulti [publicParty] [] do
    createCmd Fungible.Factory with provider = publicParty; observers = M.fromList fp

  -- Create accounts
  [senderAccount, receiverAccount] <- mapA (Account.createAccount "Cash Account" [publicParty] accountFactoryCid holdingFactoryCid [] bank) [sender, receiver]

  -- Distribute asset
  now <- getTime
  cashInstrument <- Instrument.originate cb cb "USD" "United States Doller" [] now
  transferableCid <- coerceContractId <$> Account.credit [publicParty] cashInstrument 200_000.0 senderAccount

  -- Create settlement factory
  settlementFactoryCid <- toInterfaceContractId @Factory.I <$> submitMulti [bank] [] do createCmd Factory with provider = bank; observers = empty

  -- Instruct transfer
  let step = Step with sender; receiver; quantity = Instrument.qty 200_000.0 cashInstrument
  (batchCid, [cashInstructionCid]) <- submitMulti [bank] [] do exerciseCmd settlementFactoryCid Factory.Instruct with instructors = singleton bank; settler = bank; id = Id "200000.0 USD payment"; description = "Transfer"; steps = [step]

  -- Allocate instruction
  cashInstructionCid <- submitMulti [sender] [publicParty] do exerciseCmd cashInstructionCid Instruction.Allocate with allocation = Pledge transferableCid

  -- Approve instruction
  cashInstructionCid <- submitMulti [receiver] [publicParty] do exerciseCmd cashInstructionCid Instruction.Approve with approval = TakeDelivery receiverAccount

  -- Settle batch
  [cashHoldingCid] <- submitMulti [bank] [publicParty] do exerciseCmd batchCid Batch.Settle

  -- Assert state
  let ts = [(receiver, cashHoldingCid)]
  Holding.verifyOwnerOfHolding ts
  Holding.verifyNoObservers ts

  pure ()

setupParties : Script TestParties
setupParties = do
  [cb, bank, sender, receiver, provider, publicParty] <- createParties ["CentralBank", "Bank", "Sender", "Receiver", "Provider", "PublicParty"]
  pure TestParties with cb, bank, sender, receiver, provider, publicParty
