-- Copyright (c) 2024 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Test.Util.HoldingDuplicates.BaseHolding where

-- | Test Implementation.
-- This module contains a copy of the code from the Daml.Finance.Holding package. It is used
-- specifically for testing purposes, allowing the instantiation of an alternate implementation of
-- holdings.

import DA.Set (fromList, singleton)
import Daml.Finance.Interface.Holding.V4.Holding qualified as Holding (I, View(..))
import Daml.Finance.Interface.Types.Common.V3.Types (AccountKey(..), InstrumentKey(..), PartiesMap)
import Daml.Finance.Interface.Types.Common.V3.Types qualified as HoldingStandard (HoldingStandard(..))
import Daml.Finance.Interface.Util.V3.Disclosure qualified as Disclosure (I, View(..), flattenObservers)
import Daml.Finance.Interface.Util.V3.Lockable qualified as Lockable (I, Lock(..), View(..), getLockers)
import Daml.Finance.Util.V4.Disclosure (addObserversImpl, removeObserversImpl, setObserversImpl)
import Daml.Finance.Util.V4.Lockable (acquireImpl, isValidLock, releaseImpl)

-- | Type synonym for `BaseHolding`.
type T = BaseHolding

-- | Implementation of a `BaseHolding`.
-- `BaseHolding` implements the interface `Holding.I` (which requires `Lockable.I` and
-- `Disclosure.I` to be implemented), but neither `Fungible.I` nor `Transferable.I`.
template BaseHolding
  with
    instrument : InstrumentKey
      -- ^ The instrument of which units are held.
    account : AccountKey
      -- ^ The account at which the holding is held. Defines the holding's owner and custodian.
    amount : Decimal
      -- ^ Number of units.
    lock : Optional Lockable.Lock
      -- ^ An optional lock for the holding.
    observers : PartiesMap
      -- ^ Observers.
  where
    signatory account.custodian, account.owner, Lockable.getLockers this
    observer Disclosure.flattenObservers observers

    ensure
      amount > 0.0 && isValidLock lock && instrument.holdingStandard == HoldingStandard.BaseHolding

    interface instance Disclosure.I for BaseHolding where
      view = Disclosure.View with
        disclosureControllers = fromList [account.owner, account.custodian]; observers
      setObservers = setObserversImpl @_ @Disclosure.I this None
      addObservers = addObserversImpl @_ @Disclosure.I this None
      removeObservers = removeObserversImpl @_ @Disclosure.I this None

    interface instance Lockable.I for BaseHolding where
      view = Lockable.View with lock; controllers = singleton account.owner
      acquire = acquireImpl this.lock (\lock -> this with lock)
      release = releaseImpl this.lock (\lock -> this with lock)

    interface instance Holding.I for BaseHolding where
      view = Holding.View with instrument; account; amount
