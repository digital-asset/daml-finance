-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.Instrument.Generic.Test.CallableBond where

import ContingentClaims.Core.Claim (and, at, give, one, or, scale, when)
import ContingentClaims.Core.Observation (Observation(..))
import DA.Assert ((===))
import DA.Date as D (Month(..), date)
import DA.Map qualified as M (empty, fromList)
import DA.Set (empty, fromList, singleton, toList)
import DA.Time (time)
import Daml.Finance.Holding.Fungible qualified as Fungible (Factory(..), T)
import Daml.Finance.Instrument.Generic.Election qualified as Election (Factory(..))
import Daml.Finance.Instrument.Generic.Lifecycle.Rule qualified as Lifecycle (Rule(..))
import Daml.Finance.Instrument.Generic.Test.Election.Workflow (CreateElectionCandidate(..), ValidateElectionCandidate(..), ElectionOffer(..))
import Daml.Finance.Instrument.Generic.Test.Util (originateGeneric, dateToDateClockTime, mapClaimToUTCTime)
import Daml.Finance.Interface.Account.Util (getOwner)
import Daml.Finance.Interface.Claims.Types (C, Deliverable)
import Daml.Finance.Interface.Holding.Util (getAmount)
import Daml.Finance.Interface.Instrument.Generic.Election qualified as Election (Apply(..))
import Daml.Finance.Interface.Lifecycle.Rule.Claim qualified as Claim (ClaimEffect(..), I)
import Daml.Finance.Interface.Settlement.Batch qualified as Batch (Settle(..))
import Daml.Finance.Interface.Settlement.Instruction qualified as Instruction (Allocate(..), Approve(..))
import Daml.Finance.Interface.Settlement.Types (Allocation(..), Approval(..))
import Daml.Finance.Interface.Types.Common.Types (Id(..), Parties)
import Daml.Finance.Lifecycle.Rule.Claim qualified as Claim (Rule(..))
import Daml.Finance.Settlement.Factory (Factory(..))
import Daml.Finance.Settlement.RouteProvider.SingleCustodian (SingleCustodian(..))
import Daml.Finance.Test.Util.Account qualified as Account (ControlledBy(..), createAccount, createFactory, credit)
import Daml.Finance.Test.Util.Common (createParties)
import Daml.Finance.Test.Util.Instrument qualified as Instrument (originate)
import Daml.Finance.Test.Util.Time (createDateClock)
import Daml.Script
import Prelude hiding (and, or)

-- This script tests the election mechanism on an Instrument.Generic using the example of a callable
-- bond. The custodian of the callable bond holding has the right to call back the bond and repay
-- the principal to the owner before maturity.

-- | Parties involved in the test script.
data TestParties = TestParties
  with
    bank : Party
      -- ^ Acts as custodian in the respective holdings (both cash holdings and instrument
      --   holdings). It also acts as depository in the bond instrument. It has the right to call
      --   back the bond before maturity.
    issuer : Party
      -- ^ Acts as issuer of the bond instrument. It is also the party tasked with lifecycling the
      --   contract and processing elections.
    centralBank : Party
      -- ^ The Central Bank is the depository and the issuer of the cash instrument.
    investor : Party
      -- ^ Owner of the bond holding. Chooses to exercise the right of the call bond.
    settlers : Parties
      -- ^ Any of the parties can trigger the settlement of fully allocated settlement instructions.
    publicParty : Party
      -- ^ The public party. Every party can readAs the public party.

-- | Creates the claim for a callable bond and the corresponding elections
bootstrapBond : Date -> Date -> Decimal -> Decimal -> Deliverable -> (C, C, C)
bootstrapBond intermediateDate maturity couponAmount principal ccy =
  let
    coupon = scale (Const couponAmount) $ one ccy
    called = give $ scale (Const principal) $ one ccy
    notCalled = when (at maturity) $ scale (Const principal) $ one ccy
    callableBond = when (at intermediateDate) $ and coupon $ give $ or called notCalled
  in (mapClaimToUTCTime callableBond, mapClaimToUTCTime called, mapClaimToUTCTime notCalled)

run : Script ()
run = script do

  --------------
  -- 0. SETUP --
  --------------
  TestParties{..} <- setupParties

  -- Account and holding factory
  let pp = [("PublicParty", singleton publicParty)]
  accountFactoryCid <- toInterfaceContractId <$> Account.createFactory bank pp
  holdingFactoryCid <- toInterfaceContractId <$> submit bank do
    createCmd Fungible.Factory with provider = bank; observers = M.fromList pp

  -- Create accounts
  investorAccount <- Account.createAccount "Default Account" [] accountFactoryCid holdingFactoryCid
    [] Account.Owner bank investor

  -- Originate cash
  now <- getTime
  cashInstrument <- Instrument.originate centralBank centralBank "USD" "United States Dollar" pp now

  -- Create and distribute a generic derivative
  let
    acquisitionTime = time (D.date 2022 Jul 01) 0 0 0
    intermediateDate = D.date 2022 Jul 07
    maturityDate = D.date 2023 Jul 07
    couponAmount = 1_000.0
    principalAmount = 100_000.0
    (bond, called, _) = bootstrapBond intermediateDate maturityDate couponAmount principalAmount
      cashInstrument
  genericInstrument <- originateGeneric bank issuer "ABC.DE 1% 07/07/23 Corp" "Callable Bond"
    acquisitionTime bond pp now
  investorGenericHoldingCid <- Account.credit [publicParty] genericInstrument 1.0 investorAccount

  -- Create election offer to allow the custodian to create elections
  electionFactoryCid <- submit issuer do
    toInterfaceContractId <$> createCmd Election.Factory with
      provider = issuer
      observers = M.fromList [("Custodian", singleton bank)]

  callElectionFactoryCid <- submit issuer do
    createCmd ElectionOffer with
      provider = issuer
      id = Id "bondcall1"
      description = "Bond - Call"
      claim = called
      observers = singleton publicParty
      instrument = genericInstrument
      factoryCid = electionFactoryCid

  ---------------------------------
  -- 2. ELECTION : CALL THE BOND --
  ---------------------------------

  -- One cannot exercise for more units than they own
  submitMultiMustFail [bank] [publicParty] do
    exerciseCmd callElectionFactoryCid CreateElectionCandidate with
      elector = bank
      electionTime = dateToDateClockTime intermediateDate
      holdingCid = investorGenericHoldingCid
      amount = 5000.0

  -- Create election
  electionProposalCid <- submitMulti [bank] [publicParty] do
    exerciseCmd callElectionFactoryCid CreateElectionCandidate with
      elector = bank
      electionTime = dateToDateClockTime intermediateDate
      holdingCid = fromInterfaceContractId investorGenericHoldingCid
      amount = 1.0

  currentTimeCid <- createDateClock (singleton issuer) intermediateDate empty

  electionCid <- submit issuer do
    exerciseCmd electionProposalCid ValidateElectionCandidate with
      currentTimeCid

  -- Apply election to generate new instrument version + effects
  lifecycleRuleCid <- toInterfaceContractId <$> submit bank do
    createCmd Lifecycle.Rule with
      providers = singleton bank
      observers= M.empty
      lifecycler = issuer
      id = Id "LifecycleRule"
      description = "Rule to lifecycle a generic instrument"

  (_, [effectCid]) <- submit issuer do
    exerciseCmd electionCid Election.Apply with
      observableCids = []
      exercisableCid = lifecycleRuleCid

  -- Create route provider
  routeProviderCid <- toInterfaceContractId <$> submit bank do
    createCmd SingleCustodian with provider = bank; observers = empty; custodian = bank

  -- Create settlement factory
  settlementFactoryCid <- toInterfaceContractId <$> submit bank do
    createCmd Factory with provider = bank; observers = empty

  lifecycleClaimRuleCid <- toInterfaceContractId @Claim.I <$> submit bank do
    createCmd Claim.Rule with
      providers = singleton bank
      claimers = fromList [bank, investor]
      settlers
      routeProviderCid
      settlementFactoryCid
      netInstructions = True

  -- Claim effect
  result <- submitMulti [bank] [publicParty] do
    exerciseCmd lifecycleClaimRuleCid Claim.ClaimEffect with
      claimer = bank
      holdingCids = [investorGenericHoldingCid]
      effectCid
      batchId = Id "BondCallSettlement"

  let
    [investorInstrumentInstructionCid, bankCashInstructionCouponAndPrincipalCid] =
      result.instructionCids

  -- Allocate instructions
  (investorInstrumentInstructionCid, _) <- submit investor do
    exerciseCmd investorInstrumentInstructionCid Instruction.Allocate with
      actors = singleton investor; allocation = Pledge investorGenericHoldingCid
  (bankCashInstructionCouponAndPrincipalCid, _) <- submit bank do
    exerciseCmd bankCashInstructionCouponAndPrincipalCid Instruction.Allocate with
      actors = singleton bank; allocation = CreditReceiver

  -- Approve instructions
  investorInstrumentInstructionCid <- submit bank do
    exerciseCmd investorInstrumentInstructionCid Instruction.Approve with
      actors = singleton bank; approval = DebitSender
  bankCashInstructionCouponAndPrincipalCid <- submit investor do
    exerciseCmd bankCashInstructionCouponAndPrincipalCid Instruction.Approve with
      actors = singleton investor; approval = TakeDelivery investorAccount

  -- Settle batch
  [investorCashHoldingCouponAndPrincipalCid] <- submitMulti (toList settlers) [publicParty] do
    exerciseCmd result.batchCid Batch.Settle with actors = settlers

  -- Assert state
  Some investorCouponAndPrincipal <-
    queryContractId investor $ fromInterfaceContractId @Fungible.T
      investorCashHoldingCouponAndPrincipalCid
  getAmount investorCouponAndPrincipal === couponAmount + principalAmount
  getOwner investorCouponAndPrincipal === investor

  pure ()

setupParties : Script TestParties
setupParties = do
  [bank, issuer, centralBank, investor, settler, publicParty] <-
    createParties ["Bank", "issuer", "Central Bank", "Investor", "Settler", "PublicParty"]
  pure $ TestParties with
    bank; issuer; centralBank; investor; settlers = singleton settler; publicParty
